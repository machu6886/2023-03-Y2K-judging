parsely

high

# Loss of funds for a Risk Buyer when a De-Peg event happens if the Collateral TVL is smaller than the Premium TVL.

## Summary
Loss of funds for a **Risk Buyer** when a **De-Peg** event happens if the **Collateral** TVL is smaller than the **Premium** TVL.
## Vulnerability Detail
If the balance of both vaults(Premium and Collateral) is > 0 then a **Null** epoch cannot be triggered. However if a user sees that there is a market and epoch with **Risk buyers** and a high likelihood of a De-Peg happening with no or few **Sellers(Collateral)**, they can become a **Seller** at the lowest possible deposit amount. If the De-peg event does indeed happen and the De-Peg function is triggered, the following will occur:
If the **Collateral** balance is less than the **Premium** balance, the **Buyers** actually loose funds rather than gaining funds and the **Seller** makes a profit rather than loosing funds. I have coded a PoC to help explain this.
## PoC
1. Create a new file in the test directory called **DepegTest.t.sol**
2. Copy & Paste the code below
3. Run the command **forge test -vv --match-contract DepegTest**
5. You can change the values to test with, in one place by changing the amount in the variables listed below.

Vulnerability scenario:
```solidity
    uint256 userDepositAmount = 100_000;
    uint256 user2DepositAmount = 100_000;
    uint256 sellerDepositAmount = 10_000;
    uint256 seller2DepositAmount = 10_000;
```
Working correctly as explained on website scenario:
```solidity
    uint256 userDepositAmount = 10_000;
    uint256 user2DepositAmount = 10_000;
    uint256 sellerDepositAmount = 100_000;
    uint256 seller2DepositAmount = 100_000;
```
This will give the output showing the the users lost money and the sellers actually made a profit(I have truncated for brevity):
```Text
Running 1 test for test/V2/DepegTest.t.sol:DepegTest
[PASS] testTriggerDepeg() (gas: 865840)
Logs:
  <<<---------------------- START  ----------------------------->>>
 ...
  ##################################################################
  => USER HAS MADE A **LOSS** OF :  90005
  => USER2 HAS MADE A **LOSS** OF :  90005
  => SELLER HAS MADE A ^^PROFIT^^ OF :  89950
  => SELLER2 HAS MADE A ^^PROFIT^^ OF :  89950
  <<<---------------------- END --------------------------------->>>

Test result: ok. 1 passed; 0 failed; finished in 6.49ms
```
Code:
```solidity

// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "./Helper.sol";
import "../../../src/v2/VaultV2.sol";
import "../../../src/v2/interfaces/IVaultV2.sol";
import {FixedPointMathLib} from "@solmate/utils/FixedPointMathLib.sol";

contract DepegTest is Helper {
    using FixedPointMathLib for uint256;
    VaultV2 vault;
    VaultV2 collateral;
    address treasury;
    address controller;

    uint40 begin;
    uint40 end;
    uint16 withdrawalFee;
    uint256 epochId;
    uint256 marketId;

    address premium;
    address collateralAddr;
    address public SELLER;
    address public SELLER2;
    uint256 userDepositAmount = 100_000;
    uint256 user2DepositAmount = 100_000;
    uint256 sellerDepositAmount = 10_000;
    uint256 seller2DepositAmount = 10_000;


    function setUp() public {
        SELLER = address(0x12412);
        SELLER2 = address(0x12413);
        treasury = TREASURY;
        controller = address(0x54);

        UNDERLYING = address(new MintableToken("UnderLyingToken", "utkn"));

        vault = new VaultV2(
            false,
            UNDERLYING,
            "Vault",
            "v",
            "randomURI",
            TOKEN,
            STRIKE,
            address(controller),
            TREASURY
        );
        vm.warp(120000);
        MintableToken(UNDERLYING).mint(address(this));

        collateral = new VaultV2(
            false,
            UNDERLYING,
            "Vault",
            "v",
            "randomURI",
            TOKEN,
            STRIKE,
            address(controller),
            TREASURY
        );

        vault.setCounterPartyVault(address(collateral));
        collateral.setCounterPartyVault(address(vault));

        

        MintableToken(UNDERLYING).mint(USER);
        MintableToken(UNDERLYING).mint(USER2);
        MintableToken(UNDERLYING).mint(SELLER);
        MintableToken(UNDERLYING).mint(SELLER2);

    }


    function testTriggerDepeg() public {
        uint256 userStartBalance;
        uint256 user2StartBalance;
        uint256 sellerStartBalance;
        uint256 seller2StartBalance;
        uint256 userEndBalance;
        uint256 user2EndBalance;
        uint256 sellerEndBalance;
        uint256 seller2EndBalance;

        userStartBalance = MintableToken(UNDERLYING).balanceOf(USER);
        user2StartBalance = MintableToken(UNDERLYING).balanceOf(USER2);
        sellerStartBalance = MintableToken(UNDERLYING).balanceOf(SELLER);
        seller2StartBalance = MintableToken(UNDERLYING).balanceOf(SELLER2);
        console.log("<<<---------------------- START  ----------------------------->>>");
        console.log("##################################################################");
        console.log("USER BALANCES BEFORE ANY CODE HAS RUN");
        console.log("##################################################################");
        console.log("=> USER VAULT BALANCE : ", vault.balanceOf(USER ,epochId));
        console.log("=> USER UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(USER));
        console.log("=> USER2 VAULT BALANCE : ", vault.balanceOf(USER2 ,epochId));
        console.log("=> USER2 UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(USER2));
        console.log("=> SELLER VAULT BALANCE : ", vault.balanceOf(SELLER ,epochId));
        console.log("=> SELLER UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(SELLER));
        console.log("=> SELLER2 VAULT BALANCE : ", vault.balanceOf(SELLER2 ,epochId));
        console.log("=> SELLER2 UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(SELLER2));
        console.log("");
        console.log("##################################################################"); 

        begin = uint40(block.timestamp);
        end = uint40(block.timestamp + 1 days);
        epochId = 3;

        helperSetEpoch(begin, end, epochId);

        //---------------------------------//
        // START DEPOSIT FOR USER AS BUYER //
        //---------------------------------//
        vm.startPrank(USER);
        MintableToken(UNDERLYING).approve(address(vault), userDepositAmount);
        // deposit tokens
        console.log("=> USER balance before deposit", MintableToken(UNDERLYING).balanceOf(USER));
        vault.deposit(epochId, userDepositAmount, USER);
        
        console.log("=> USER balance after deposit", MintableToken(UNDERLYING).balanceOf(USER));
        vm.stopPrank();
        //---------------------------------//
        //      END DEPOSIT FOR USER       //
        //---------------------------------//

        //---------------------------------//
        // START DEPOSIT FOR USER2 AS BUYER//
        //---------------------------------//
        vm.startPrank(USER2);
        MintableToken(UNDERLYING).approve(address(vault), user2DepositAmount);
        // deposit tokens
        console.log("=> USER2 balance before deposit", MintableToken(UNDERLYING).balanceOf(USER2));
        vault.deposit(epochId, user2DepositAmount, USER2);
        
        console.log("=> USER2 balance after deposit", MintableToken(UNDERLYING).balanceOf(USER2));
        vm.stopPrank();
        //---------------------------------//
        //      END DEPOSIT FOR USER2      //
        //---------------------------------//

        //----------------------------------//
        //START DEPOSIT FOR SELLER AS SELLER//
        //----------------------------------//
        vm.startPrank(SELLER);
        MintableToken(UNDERLYING).approve(address(collateral), sellerDepositAmount);
        // deposit tokens
        console.log("=> SELLER balance before deposit", MintableToken(UNDERLYING).balanceOf(SELLER));
        collateral.deposit(epochId, sellerDepositAmount, SELLER);
        
        console.log("=> SELLER balance after deposit", MintableToken(UNDERLYING).balanceOf(SELLER));
        vm.stopPrank();
        //---------------------------------//
        //      END DEPOSIT FOR SELLER     //
        //---------------------------------//

        //-----------------------------------//
        //START DEPOSIT FOR SELLER AS SELLER2//
        //-----------------------------------//
        vm.startPrank(SELLER2);
        MintableToken(UNDERLYING).approve(address(collateral), sellerDepositAmount);
        // deposit tokens
        console.log("=> SELLER2 balance before deposit", MintableToken(UNDERLYING).balanceOf(SELLER2));
        collateral.deposit(epochId, sellerDepositAmount, SELLER2);
        
        console.log("=> SELLER2 balance after deposit", MintableToken(UNDERLYING).balanceOf(SELLER2));
        vm.stopPrank();
        //---------------------------------//
        //      END DEPOSIT FOR SELLER2    //
        //---------------------------------//




        vm.startPrank(controller);
        //----------------------------------------//
        //     START OF DEPEG CODE COPIED         //
        //----------------------------------------//
        vault.resolveEpoch(epochId);
        collateral.resolveEpoch(epochId);

        uint256 epochFee = uint256(uint16(0x5));

        uint256 premiumTVL = vault.finalTVL(epochId);
        uint256 collateralTVL = collateral.finalTVL(epochId);

        uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);
        uint256 collateralFee = calculateWithdrawalFeeValue(
            collateralTVL,
            epochFee
        );

        // avoid stack too deep error by avoiding local variables
        // uint256 premiumTVLAfterFee = premiumTVL - premiumFee;
        // uint256 collateralTVLAfterFee = collateralTVL - collateralFee;

        vault.setClaimTVL(epochId, collateralTVL - collateralFee);
        collateral.setClaimTVL(epochId, premiumTVL - premiumFee);

        // send fees to treasury and remaining TVL to respective counterparty vault
        // strike price reached so premium is entitled to collateralTVL - collateralFee
        vault.sendTokens(epochId, premiumFee, treasury);
        vault.sendTokens(
            epochId,
            premiumTVL - premiumFee,
            address(collateral)
        );
        // strike price is reached so collateral is still entitled to premiumTVL - premiumFee but looses collateralTVL
        collateral.sendTokens(epochId, collateralFee, treasury);
        collateral.sendTokens(
            epochId,
            collateralTVL - collateralFee,
            address(vault)
        );
        //----------------------------------------//
        //     END OF DEPEG CODE COPIED           //
        //----------------------------------------//
        vm.stopPrank();
        
        console.log("##################################################################");
        console.log("Vault values after depeg event");
        console.log("##################################################################");
        console.log("Vault PreviewWithdraw value : ", vault.previewWithdraw(epochId,userDepositAmount));        
        console.log("Vault claimTVL : ", vault.claimTVL(epochId));
        console.log("Vault finalTVL : ", vault.finalTVL(epochId));
        console.log("");
        console.log("##################################################################");
        console.log("");
        console.log("##################################################################");
        console.log("Collateral values after depeg event");
        console.log("##################################################################");
        console.log("Colateral PreviewWithdraw value : ", collateral.previewWithdraw(epochId,sellerDepositAmount));
        console.log("Collateral claimTVL : ", collateral.claimTVL(epochId));
        console.log("Collateral finalTVL : ", collateral.finalTVL(epochId));
        console.log("");
        console.log("##################################################################");
        console.log("USER BALANCES BEFORE WITHDRAW");
        console.log("##################################################################");
        console.log("USER VAULT BALANCE : ", vault.balanceOf(USER ,epochId));
        console.log("USER UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(USER));
        console.log("USER2 VAULT BALANCE : ", vault.balanceOf(USER2 ,epochId));
        console.log("USER2 UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(USER2));
        console.log("SELLER VAULT BALANCE : ", collateral.balanceOf(SELLER ,epochId));
        console.log("SELLER UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(SELLER));
        console.log("SELLER2 VAULT BALANCE : ", collateral.balanceOf(SELLER2 ,epochId));
        console.log("SELLER2 UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(SELLER2));
        console.log("");
        console.log("##################################################################");
        vm.startPrank(USER);
        vault.withdraw(epochId, userDepositAmount, USER, USER);
        vm.stopPrank();
        vm.startPrank(USER2);
        vault.withdraw(epochId, user2DepositAmount, USER2, USER2);
        vm.stopPrank();
        vm.startPrank(SELLER);
        collateral.withdraw(epochId, sellerDepositAmount, SELLER, SELLER);
        vm.stopPrank();
        vm.startPrank(SELLER2);
        collateral.withdraw(epochId, sellerDepositAmount, SELLER2, SELLER2);
        vm.stopPrank();

        console.log("##################################################################");
        console.log("USER BALANCES AFTER WITHDRAW");
        console.log("##################################################################");
        console.log("USER VAULT BALANCE : ", vault.balanceOf(USER ,epochId));
        console.log("USER UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(USER));
        console.log("USER2 VAULT BALANCE : ", vault.balanceOf(USER2 ,epochId));
        console.log("USER2 UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(USER2));
        console.log("SELLER VAULT BALANCE : ", vault.balanceOf(SELLER ,epochId));
        console.log("SELLER UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(SELLER));
        console.log("SELLER2 VAULT BALANCE : ", vault.balanceOf(SELLER2 ,epochId));
        console.log("SELLER2 UNDERLYING BALANCE : ", MintableToken(UNDERLYING).balanceOf(SELLER2));
        console.log("");
        console.log("##################################################################");
        userEndBalance = MintableToken(UNDERLYING).balanceOf(USER);
        user2EndBalance = MintableToken(UNDERLYING).balanceOf(USER2);
        sellerEndBalance = MintableToken(UNDERLYING).balanceOf(SELLER);
        seller2EndBalance = MintableToken(UNDERLYING).balanceOf(SELLER2);


        if(userEndBalance > userStartBalance)
        {
            console.log("=> USER HAS MADE A ^^PROFIT^^ OF : ", userEndBalance - userStartBalance);
        }else{
            console.log("=> USER HAS MADE A **LOSS** OF : ", userStartBalance - userEndBalance);
        }
        if(user2EndBalance > user2StartBalance)
        {
            console.log("=> USER2 HAS MADE A ^^PROFIT^^ OF : ", user2EndBalance - user2StartBalance);
        }else{
            console.log("=> USER2 HAS MADE A **LOSS** OF : ", user2StartBalance - user2EndBalance);
        }
        if(sellerEndBalance > sellerStartBalance)
        {
            console.log("=> SELLER HAS MADE A ^^PROFIT^^ OF : ", sellerEndBalance - sellerStartBalance);
        }else{
            console.log("=> SELLER HAS MADE A **LOSS** OF : ", sellerStartBalance - sellerEndBalance);
        }
        if(seller2EndBalance > seller2StartBalance)
        {
            console.log("=> SELLER2 HAS MADE A ^^PROFIT^^ OF : ", seller2EndBalance - seller2StartBalance);
        }else{
            console.log("=> SELLER2 HAS MADE A **LOSS** OF : ", seller2StartBalance - seller2EndBalance);
        }


        console.log("<<<---------------------- END --------------------------------->>>");
    }

    function calculateWithdrawalFeeValue(uint256 amount, uint256 fee)
        public
        pure
        returns (uint256 feeValue)
    {
        // 0.5% = multiply by 10000 then divide by 50
        return amount.mulDivDown(fee, 10000);
    }

    function helperSetEpoch(
        uint40 _epochBegin,
        uint40 _epochEnd,
        uint256 _epochId
    ) public {
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        collateral.setEpoch(_epochBegin, _epochEnd, _epochId);
    }

}
```
## Impact
As a **Buyer** the user is expecting to at least not loose funds if a de-peg event happens, and a **Seller** should loose collateral, but the current code does not work as such.
## Code Snippet
https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Controllers/ControllerPeggedAssetV2.sol#L51-L138
In ControllerPeggedAssetV2.sol the function ```previewWithdraw```
```solidity
function triggerDepeg(uint256 _marketId, uint256 _epochId) public {
        address[2] memory vaults = vaultFactory.getVaults(_marketId);

        if (vaults[0] == address(0) || vaults[1] == address(0))
            revert MarketDoesNotExist(_marketId);

        IVaultV2 premiumVault = IVaultV2(vaults[0]);
        IVaultV2 collateralVault = IVaultV2(vaults[1]);

        if (premiumVault.epochExists(_epochId) == false) revert EpochNotExist();

        int256 price = getLatestPrice(premiumVault.token());

        if (int256(premiumVault.strike()) <= price)
            revert PriceNotAtStrikePrice(price);

        (uint40 epochStart, uint40 epochEnd, ) = premiumVault.getEpochConfig(
            _epochId
        );

        if (uint256(epochStart) > block.timestamp) revert EpochNotStarted();

        if (block.timestamp > uint256(epochEnd)) revert EpochExpired();

        //require this function cannot be called twice in the same epoch for the same vault
        if (premiumVault.epochResolved(_epochId)) revert EpochFinishedAlready();
        if (collateralVault.epochResolved(_epochId))
            revert EpochFinishedAlready();

        // check if epoch qualifies for null epoch
        if (
            premiumVault.totalAssets(_epochId) == 0 ||
            collateralVault.totalAssets(_epochId) == 0
        ) {
            revert VaultZeroTVL();
        }

        premiumVault.resolveEpoch(_epochId);
        collateralVault.resolveEpoch(_epochId);

        uint256 epochFee = vaultFactory.getEpochFee(_epochId);

        uint256 premiumTVL = premiumVault.finalTVL(_epochId);
        uint256 collateralTVL = collateralVault.finalTVL(_epochId);

        uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);
        uint256 collateralFee = calculateWithdrawalFeeValue(
            collateralTVL,
            epochFee
        );

        // avoid stack too deep error by avoiding local variables
        // uint256 premiumTVLAfterFee = premiumTVL - premiumFee;
        // uint256 collateralTVLAfterFee = collateralTVL - collateralFee;

        premiumVault.setClaimTVL(_epochId, collateralTVL - collateralFee);
        collateralVault.setClaimTVL(_epochId, premiumTVL - premiumFee);

        // send fees to treasury and remaining TVL to respective counterparty vault
        // strike price reached so premium is entitled to collateralTVL - collateralFee
        premiumVault.sendTokens(_epochId, premiumFee, treasury);
        premiumVault.sendTokens(
            _epochId,
            premiumTVL - premiumFee,
            address(collateralVault)
        );
        // strike price is reached so collateral is still entitled to premiumTVL - premiumFee but looses collateralTVL
        collateralVault.sendTokens(_epochId, collateralFee, treasury);
        collateralVault.sendTokens(
            _epochId,
            collateralTVL - collateralFee,
            address(premiumVault)
        );

        emit EpochResolved(
            _epochId,
            _marketId,
            VaultTVL(
                premiumTVL - premiumFee,
                collateralTVL,
                collateralTVL - collateralFee,
                premiumTVL
            ),
            true,
            block.timestamp,
            price
        );
    }
 ```
## Tool used
Manual Review

## Recommendation
Allow the **Buyer** to stop the process if this scenario happens, or only allow a valid Epoch to start if the **Collateral** balance is greater than the **Premium** balance as even if they are equal, both parties just loose the fees part of their deposit.