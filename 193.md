auditor0517

high

# Wrong update of `ownerToRollOverQueueIndex`

## Summary
In `Carousel.enlistInRollover()`, it updates `ownerToRollOverQueueIndex` wrongly when the index exists already.

## Vulnerability Detail
Users can enlist in the rollover queue using `enlistInRollover()`.

```solidity
    function enlistInRollover(
        uint256 _epochId,
        uint256 _assets,
        address _receiver
    ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
        // check if sender is approved by owner
        if (
            msg.sender != _receiver &&
            isApprovedForAll(_receiver, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
        // check if user has enough balance
        if (balanceOf(_receiver, _epochId) < _assets)
            revert InsufficientBalance();

        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
        }
        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length; //@audit wrong update

        emit RolloverQueued(_receiver, _assets, _epochId);
    }
```

But it updates the index when the receiver is inside the queue already.

So `ownerToRollOverQueueIndex` will be tracked wrongly if the already added user enlists again.

## Impact
The `rollover` logic of the protocol will be broken because it tracks the rollover index wrongly.

## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L268

## Tool used
Manual Review

## Recommendation
`ownerToRollOverQueueIndex` should be updated only when the receiver is added for the first time.

```solidity
        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );

            ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
        }
```