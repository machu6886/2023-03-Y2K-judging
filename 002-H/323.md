Junnon

high

# `enlistInRollover()` can change other user `rolloverQueue`

## Summary
`enlistInRollover()` function inside Carousel.sol can change other user `rolloverQueue` without approval.
## Vulnerability Detail
`enlistInRollover()` handled the `rolloverQueue` update incorrectly. check the code snippets bellow 
```js
File: Carousel.sol
        ...
253:        if (ownerToRollOverQueueIndex[_receiver] != 0) {
254:            // if so, update the queue
255:            uint256 index = getRolloverIndex(_receiver);
256:            rolloverQueue[index].assets = _assets;
257:            rolloverQueue[index].epochId = _epochId;
258:        } else {
259:            // if not, add to queue
260:            rolloverQueue.push(
261:                QueueItem({
262:                    assets: _assets,
263:                    receiver: _receiver,
264:                    epochId: _epochId
265:                })
266:            );
267:        }
268:        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
        ...
```

according to code above, if `_receier` has been enlisted before his/her `rolloverQueue` will be updated with new data. But in there, `ownerToRollOverQueueIndex[_receiver]` updated every time the function called it will make `getRolloverIndex(_receiver)` return the latest number of the array, which is not the `_receiver` own queue. Therefore, instead updating the receiver queue data this function will constantly updating other user queue (mostly the latest user enlisted). 

## Impact
this bug can caused several other bug inside the contract, which is: 
1. DOS on `mintRollovers()`
consider this following scenario: 
    1. A malicious user enlist 2 address, address A with balance of 2 ethers ERC1155 NFT and address B with balance of 1 wei more than relayerFee ERC1155 NFT. 
    2. user enlist address A by calling `enlistInRollover()` with 2 ethers 
    3. user enlist address B with 1 wei more than relayerFee 
    4. user call `enlistInRollover()` with address A as `_receiver` 2 times to update `rolloverQueue` of address B to 2 ether 
    5. now `mintRollovers()` will always revert since address B don't have 2 ether balance of ERC1155 NFT to be burn when `mintRollovers()` called
This DOS cannot be resolved until that malicious user delist their account. which is not under the protocol control. 

2. Arbitary user can block transfer of latest user enlisted
because `safeTransferFrom()` using `notRollingOver` modifier if the data of their `rolloverQueue` is more than their balance, they cannot transfer the NFT until they delist from rollover

3. Arbitary user can change latest user `rolloverQueue` data that make inconvinient for other user.

### Code POC
insert this code below to CarouselTest.t.sol
```js
    function testEnListInRollover_POC() public {
        // create two epochs
        uint40 _epochBegin = uint40(block.timestamp + 1 days);
        uint40 _epochEnd = uint40(block.timestamp + 2 days);
        uint256 _epochId = 2;
        uint256 _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);
    
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);

        vm.warp(_epochBegin - 10 minutes);
    
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);
        helperDepositInEpochs(_epochId,USER3, false);
        helperDepositInEpochs(_epochId,USER4, false);

        // enlist in rollover for next epoch
        vm.startPrank(USER);
        //_epochId == epoch user is depositing in / amount of shares he wants to rollover
        vault.enlistInRollover(_epochId, 8 ether, USER);
        vm.stopPrank();

        //enlist USER2, USER3, USER4
        vm.startPrank(USER2);
        vault.enlistInRollover(_epochId, 4 ether, USER2);
        vm.stopPrank();
        vm.startPrank(USER3);
        vault.enlistInRollover(_epochId, 3 ether, USER3);
        vm.stopPrank();
        vm.startPrank(USER4);
        vault.enlistInRollover(_epochId, 1 ether, USER4);
        vm.stopPrank();

        //USER TRY TO UPDATE THEIR QUEUE DATA 2 times
        vm.startPrank(USER);
        vault.enlistInRollover(_epochId, 11 ether, USER);
        vault.enlistInRollover(_epochId, 11 ether, USER);
        vm.stopPrank();

        //Fetch Data Of rolloverQueue of USER
        (uint256 assets_, address receiver_, uint256 epochId_) = vault.rolloverQueue(3);

        console.log(assets_); // 11 ether
        console.log(USER); // == USER
        console.log(receiver_); // == USER4 instead of USER
        console.log(epochId_); // 2 
    }
```
## Code Snippets
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L232-L271

## Tool used

Manual Review

## Recommendation

in `enlistInRollover()` update the `ownerToRollOverQueueIndex` only if the `_receiver` not enlisted yet
```diff
File: Carousel.sol
    function enlistInRollover(
        uint256 _epochId,
        uint256 _assets,
        address _receiver
    ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
        // check if sender is approved by owner
        if (
            msg.sender != _receiver &&
            isApprovedForAll(_receiver, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
        // check if user has enough balanceepochHasEnded
        if (balanceOf(_receiver, _epochId) < _assets)
            revert InsufficientBalance();

        // check if user has already queued up a rollover
       if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
++          ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
        }
--      ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

        emit RolloverQueued(_receiver, _assets, _epochId);
    }
```
