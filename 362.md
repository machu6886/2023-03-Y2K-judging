ck

high

# `ownerToRollOverQueueIndex` is incorrectly updated if user already in queue

## Summary

`ownerToRollOverQueueIndex` is incorrectly updated if user already in queue

## Vulnerability Detail

In the `enlistInRollover` function, there is a check whether a user is already queued for a rollover: 

```solidity
    function enlistInRollover(
        uint256 _epochId,
        uint256 _assets,
        address _receiver
    ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
        // check if sender is approved by owner
        if (
            msg.sender != _receiver &&
            isApprovedForAll(_receiver, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
        // check if user has enough balance
        if (balanceOf(_receiver, _epochId) < _assets)
            revert InsufficientBalance();

        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
        }
        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

        emit RolloverQueued(_receiver, _assets, _epochId);
    }
```

The issue is that if they are already queued, which means that they exist in `ownerToRollOverQueueIndex`, their index will be incorrectly updated at the end of the function despite them not being pushed to the end of the queue.

```solidity
ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```

The update should only be done if they didn't exist in `ownerToRollOverQueueIndex`

## Impact

A user will be incorrectly mapped to the wrong rollover and lead to them being minted different shares than what they enlisted for.

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L238-L271

## Tool used

Manual Review

## Recommendation

Move the update ` ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;` into the else condition where a push to the queue is done. 