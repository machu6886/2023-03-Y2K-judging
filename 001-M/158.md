Ruhum

medium

# Increasing a carousel's relayerFee can cause the deposit queue to fail

## Summary
If the owner increases a carousel's relayer fee, a deposit might underflow causing the queue to halt.

## Vulnerability Detail
When a user makes a deposit they are forced to deposit a minimum amount through the `minRequiredDeposit()` modifier:
```sol
    function deposit(
        uint256 _id,
        uint256 _assets,
        address _receiver
    )
        public
        override(VaultV2)
        epochIdExists(_id)
        epochHasNotStarted(_id)
        minRequiredDeposit(_assets)
        nonReentrant
    {}

    modifier minRequiredDeposit(uint256 _assets) {
        if (_assets < relayerFee) revert MinDeposit();
        _;
    }
```
They have to at least deposit `relayerFee` amount. Given that relayerFee is 100,000 and Alice deposits exactly 100,000 tokens through the queue. If the `relayerFee` is increased by the admin **before** Alice's deposit is processed, it can cause the function to revert because of an underflow in [L337](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L337):

```sol
    function mintDepositInQueue(uint256 _epochId, uint256 _operations)
        external
        epochIdExists(_epochId)
        epochHasNotStarted(_epochId)
        nonReentrant
    {
        // make sure there is already a new epoch set
        // epoch has not started
        QueueItem[] memory queue = depositQueue;
        uint256 length = depositQueue.length;

        // dont allow minting if epochId is 0
        if (_epochId == 0) revert InvalidEpochId();

        if (length == 0) revert OverflowQueue();
        // relayers can always input a very big number to mint all deposit queues, without the need to read depostQueue length first
        if (_operations > length) _operations = length;

        // queue is executed from the tail to the head
        // get last index of queue
        uint256 i = length - 1;
        while ((length - _operations) <= i) {
            // this loop impelements FILO (first in last out) stack to reduce gas cost and improve code readability
            // changing it to FIFO (first in first out) would require more code changes and would be more expensive
            _mintShares(
                queue[i].receiver,
                _epochId,
                // @audit this can underflow if assets < relayerFee
                queue[i].assets - relayerFee
            );
            emit Deposit(
                msg.sender,
                queue[i].receiver,
                _epochId,
                queue[i].assets - relayerFee
            );
            depositQueue.pop();
            if (i == 0) break;
            unchecked {
                i--;
            }
        }

        emit RelayerMinted(_epochId, _operations);

        asset.safeTransfer(msg.sender, _operations * relayerFee);
    }
```

`queue[i].assets` is the amount the user deposited, e.g. 100,000. If relayerFee is now increased so that `assets < relayerFee` the function will revert.

Deposits are processed using FILO. There's no way to cancel a deposit. So if this happens, any deposit made before Alice's will be locked up until the relayerFee is decreased again.

## Impact
Deposits made before the one that causes the revert will be locked up.

## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L337
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L602

## Tool used

Manual Review

## Recommendation
All deposits should be processed before the relayer's fee is updated.
