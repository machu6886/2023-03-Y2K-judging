shaka

medium

# Relayer fee increase can cause `mintDepositInQueue()` to fail

## Summary

If the `relayerFee` is increased in a `Carousel` the next execution of `mintDepositInQueue()` might fail.

## Vulnerability Detail

When a deposit is made there is a check that ensures that the deposit can cover the `relayerFee`.

```solidity
 78     function deposit(
 79         uint256 _id,
 80         uint256 _assets,
 81         address _receiver
 82     )
 83         public
 84         override(VaultV2)
 85         epochIdExists(_id)
 86         epochHasNotStarted(_id)
 87         minRequiredDeposit(_assets)
 88         nonReentrant
 89     {
 
		// (...)
		
739     modifier minRequiredDeposit(uint256 _assets) {
740         if (_assets < relayerFee) revert MinDeposit();
741         _;
742     }
```

However, the `relayerFee` can be increased after the deposit is made and before the relayer mints the deposits in queue.

```solidity
602     function changeRelayerFee(uint256 _relayerFee) external onlyFactory {
603         relayerFee = _relayerFee;
604     }
```

If a user queued a deposit with an amount lower than the new `relayerFee`, `mintDepositInQueue()` will revert due to an underflow error in line 337.

```solidity
334     _mintShares(
335         queue[i].receiver,
336         _epochId,
337         queue[i].assets - relayerFee
338     );
```

## Impact

As a FILO strategy is used, all the deposits with an index lower than the one of the user that does not have enough assets will be locked until the  `relayerFee` is lowered again. Given that this value is set by the `Timelocker`, the deposits might stay locked for several days and unable to be included in the current epoch.

A user can exploit this in the following way:
- The user notices that there is a change scheduled by the `Timelocker` that will increase the `relayerFee` of a certain `Market`.
- Before the value of `relayerFee` is changed, the user queues a deposit with a value equal to the current `relayerFee`.
- The user manages to lock for the epoche all deposits made before him, removing part of the "competition" for the premium rewards.
- The user deposits funds for the current epoch.

Also, it might be considered unfair for the users to change the `relayerFee` that they will have to pay after they have made the deposit with a different value, especially given that they cannot revert the operation.

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L602-L604
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L337

## Proof of concept

```solidity
function testMintDepositInQueueFails() public {
    uint40 _epochBegin = uint40(block.timestamp + 1 days);
    uint40 _epochEnd = uint40(block.timestamp + 2 days);
    uint256 _epochId = 1;
    uint256 _emissions = 100 ether;

    deal(emissionsToken, address(vault), 100 ether, true);
    vault.setEpoch(_epochBegin, _epochEnd, _epochId);
    vault.setEmissions( _epochId, _emissions);

    vm.startPrank(USER);
    IERC20(UNDERLYING).approve(address(vault), 10 ether);
    vault.deposit(0, 1 ether, USER);
    vm.stopPrank();

    uint256 user2Amount = relayerFee;
    vm.startPrank(USER2);
    IERC20(UNDERLYING).approve(address(vault), 10 ether);
    vault.deposit(0, user2Amount, USER2);
    vm.stopPrank();

    // Increase relayer fee by 1 wei
    vault.changeRelayerFee(relayerFee + 1);
    relayerFee = vault.relayerFee();

    vm.expectRevert();
    vault.mintDepositInQueue(_epochId, 230000);
}
```

## Tool used

Manual Review

## Recommendation

In the epoche when the change has been made, use the minimum of the previous `relayerFee` and current `relayerFee` to ensure all deposits can cover it.

An alternative solution is handling the underflow error in `mintDepositInQueue` and skipping deposits without enough assets to cover the relayer fee.