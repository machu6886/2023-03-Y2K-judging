climber2002

high

# In Carousel.enlistInRollover the update of ownerToRollOverQueueIndex is wrong if the user has already queued up a rollover

## Summary

In Carousel.enlistInRollover the [update of ownerToRollOverQueueIndex](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L268) is wrong if the user has already queued up a rollover

## Vulnerability Detail
In [Carousel.enlistInRollover](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L252-L268) it has following code,

```solidity
        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
        }
        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```

When user already has a queued up rollover, aka `ownerToRollOverQueueIndex[_receiver] != 0`, it just gets the `index` for the user and update assets and epochId on `rolloverQueue[index]` on the fly. So when `if` is true, it shouldn't update `ownerToRollOverQueueIndex[_receiver]`. However `ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;` is called for both `if` and `else` branch.

## Impact
This is a high vulnerability as if the `index` is wrong, it could point to a different user. So when a user performs an operation, it could actually perform the operation on another user, which totally breaks the logic.

## Code Snippet
I created a test In `CarouselTest`, it performs following logic,
1. enlist `USER`, which makes `getRolloverIndex(USER)` return `0`
2. enlist `USER2`, which makes `getRolloverIndex(USER2)` returns `1`
3. enlist `USER` again, now both `getRolloverIndex(USER)` and `getRolloverIndex(USER2)` return `1`, which is not correct.

```solidity
  function testEnListInRollover_userAlreadyQueuedUpRollover() public {
        // create two epochs
        uint40 _epochBegin = uint40(block.timestamp + 1 days);
        uint40 _epochEnd = uint40(block.timestamp + 2 days);
        uint256 _epochId = 2;
        uint256 _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);
    
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);

        vm.warp(_epochBegin - 10 minutes);
    
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);

        // enlist in rollover for next epoch
        vm.startPrank(USER);
        //_epochId == epoch user is depositing in / amount of shares he wants to rollover
        vault.enlistInRollover(_epochId, 8 ether, USER);
        vm.stopPrank();

        assertEq(vault.getRolloverIndex(USER), 0);

        // user2 enlists in rollover
        vm.startPrank(USER2);
        vault.enlistInRollover(_epochId, 1 ether, USER2);
        vm.stopPrank();

        assertEq(vault.getRolloverIndex(USER2), 1);

        // Now enlist USER again
        vm.startPrank(USER);
        vault.enlistInRollover(_epochId, 8 ether, USER);
        vm.stopPrank();
        
         // here vault.getRolloverIndex(USER) should be 0
        assertEq(vault.getRolloverIndex(USER), 1);
        assertEq(vault.getRolloverIndex(USER2), 1);
    }
```

## Tool used

Manual Review

## Recommendation

Put ` ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;` in `else` branch only, like following,

```solidity
        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
           ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
        }
        
```