Respx

medium

# Users can avoid all deposit fees by depositing through the carousel queue and immediately relaying

## Summary
The queue system in `Carousel` does not charge any treasury fee. The treasury fee increases as the deposit deadline nears to offset the information advantage of late depositors. Depositors in the queue do pay a relayer fee. 

However, because the queue operates on a FILO (first in last out) basis, it is possible for a user to add their deposit to the queue and then immediately (even within the same transaction) relay their own transaction and collect their own relayer fee, thus avoiding paying any fees at all.

## Vulnerability Detail
When a user makes a direct deposit, they are charged a fee as defined in `Carousel_calculateFeePercent()` which increases with time. This is because, as the [code comments say](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L482), 
> late depositors have an informational advantage

However, a user who deposits into epoch zero enters the carousel's queue system, in which any user, acting as a relayer, can call `mintDepositInQueue()` to transfer the last deposit(s) in the queue into any epoch which is accepting deposits. In this case, no treasury fee is paid. The depositing user only pays a fee to the relayer of `relayerFee`. 

It is therefore possible for a user to call `Carousel.deposit()` with an `_id` (epoch id) of zero, and be added to the end of the carousel queue by the code on [line 495](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L495) and then, within the same transaction, call `mintDepositInQueue()` with an `_operations` argument of 1. As the comments in this function state, the last item in the queue is processed first. This means that the user would relay their own deposit and would be paid their own relayer fee.

The user has thereby managed to deposit whilst paying no fee.

In terms of the code, the FILO function is implemented by the iterator `i` in the loop on [line 331](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L331). This is set to the index of the last item in the queue, and the loop then processes this item and deposits into the current open epoch. The loop reduces the deposit amount by `relayerFee` on [line 337](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L337), but this in in a call to `_mintShares()`, a function which directly mints shares and does not charge any treasury fee.

`relayerFee` is then paid to `msg.sender` on [line 354](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L354) (multiplied by the number of operations processed).

## Impact
- Any user who enters via the queue avoids paying the treasury fee, which means the protocol treasury loses out.
- The treasury fee also serves to balance the information advantage of late depositors. Circumventing this will penalise early depositors who will have all of the downsides of early depositing and none of the upside from lower fees.
- This will hurt the protocol by potentially lowering the total number of depositors as some who might have deposited earlier may not deposit at all. The lack of early deposits will then disincentivise later potential depositors.
- Even if it is intentional for queue depositors to pay no treasury fee, this workaround would create far more queue depositors than otherwise and would thereby reduce treasury fees, harming the protocol.

## Code Snippet
https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L470-L501
```Solidity
    function _deposit(
        uint256 _id,
        uint256 _assets,
        address _receiver
    ) internal {
        // mint logic, either in queue or direct deposit
        if (_id != 0) {
            uint256 assetsToDeposit = _assets;

            if (depositFee > 0) {
                (uint256 maxX, , uint256 minX) = getEpochConfig(_id);
                // deposit fee is calcualted linearly between time of epoch creation and epoch starting (deposit window)
                // this is because late depositors have an informational advantage
                uint256 fee = _calculateFeePercent(int256(minX), int256(maxX));
                // min minRequiredDeposit modifier ensures that _assets has high enough value to not devide by 0
                // 0.5% = multiply by 10000 then divide by 50
                uint256 feeAmount = _assets.mulDivDown(fee, 10000);
                assetsToDeposit = _assets - feeAmount;
                _asset().safeTransfer(treasury, feeAmount);
            }

            _mintShares(_receiver, _id, assetsToDeposit);

            emit Deposit(msg.sender, _receiver, _id, _assets);
        } else {
            depositQueue.push(
                QueueItem({assets: _assets, receiver: _receiver, epochId: _id})
            );

            emit DepositInQueue(msg.sender, _receiver, _id, _assets);
        }
    }
```
Note the treasury fee and the `else` block where no fee is paid but the deposit is added to the queue. Then consider the queue processing function:
https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L310-L350
```Solidity
    function mintDepositInQueue(uint256 _epochId, uint256 _operations)
        external
        epochIdExists(_epochId)
        epochHasNotStarted(_epochId)
        nonReentrant
    {
        // make sure there is already a new epoch set
        // epoch has not started
        QueueItem[] memory queue = depositQueue;
        uint256 length = depositQueue.length;


        // dont allow minting if epochId is 0
        if (_epochId == 0) revert InvalidEpochId();


        if (length == 0) revert OverflowQueue();
        // relayers can always input a very big number to mint all deposit queues, without the need to read depostQueue length first
        if (_operations > length) _operations = length;


        // queue is executed from the tail to the head
        // get last index of queue
        uint256 i = length - 1;
        while ((length - _operations) <= i) {
            // this loop impelements FILO (first in last out) stack to reduce gas cost and improve code readability
            // changing it to FIFO (first in first out) would require more code changes and would be more expensive
            _mintShares(
                queue[i].receiver,
                _epochId,
                queue[i].assets - relayerFee
            );
            emit Deposit(
                msg.sender,
                queue[i].receiver,
                _epochId,
                queue[i].assets - relayerFee
            );
            depositQueue.pop();
            if (i == 0) break;
            unchecked {
                i--;
            }
        }
```

## Tool used

Manual Review

## Recommendation
Consider disallowing deposits into the queue when there is an epoch open and accepting direct deposits.
Consider charging a treasury fee on queue deposits in addition to the relayer fee.
