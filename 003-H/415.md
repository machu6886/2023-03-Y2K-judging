berndartmueller

high

# Delisting a rollover queue item reorders an item and prevents the reordered item from being rolled over in the next epoch

## Summary

Delisting a carousel rollover queue item will reorder the rollover queue and prevent the last item in the queue from being rolled over if the `rolloverAccounting[_epochId]` index for a given epoch is greater than the reordered index.

## Vulnerability Detail

Delisting from the carousel rollover queue via the `Carousel.delistInRollover` function reorders the queue by moving the last item in the queue to the index of the item that is being removed.

Processing rollover queue items with the `Carousel.mintRollovers` function [stores the index of the last processed rollover queue item](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L453) in the `rolloverAccounting[_epochId]` mapping. The next time `Carousel.mintRollovers` is called, it will start processing the rollover queue from the index stored in `rolloverAccounting[_epochId]`.

Assume the rollover queue is being processed for a given epoch in batches of `_operations = 100` items and the `rolloverAccounting[_epochId]` index is set to `100`, delisting the rollover queue item with index `50` in between the `mintRollovers` calls will move the last item in the queue to index `50`. This will prevent the just reordered item (at index `50`) from being rolled due to `rolloverAccounting[_epochId] > 50`.

## Impact

The owner of the reordered item will be silently excluded from the next epoch while the owner assumes to be hedged.

## Code Snippet

[src/v2/Carousel/Carousel.delistInRollover](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L293-L300)

```solidity
276: function delistInRollover(address _owner) public {
277:     // check if user has already queued up a rollover
278:     if (ownerToRollOverQueueIndex[_owner] == 0) revert NoRolloverQueued();
279:     // check if sender is approved by owner
280:     if (
281:         msg.sender != _owner &&
282:         isApprovedForAll(_owner, msg.sender) == false
283:     ) revert OwnerDidNotAuthorize(msg.sender, _owner);
284:
285:     // swich the last item in the queue with the item to be removed
286:     uint256 index = getRolloverIndex(_owner);
287:     uint256 length = rolloverQueue.length;
288:     if (index == length - 1) {
289:         // if only one item in queue
290:         rolloverQueue.pop();
291:         delete ownerToRollOverQueueIndex[_owner];
292:     } else {
293:         // overwrite the item to be removed with the last item in the queue
294: @>      rolloverQueue[index] = rolloverQueue[length - 1];
295: @>      // remove the last item in the queue
296: @>      rolloverQueue.pop();
297: @>      // update the index of prev last user ( mapping index is allways array index + 1)
298: @>      ownerToRollOverQueueIndex[rolloverQueue[index].receiver] =
299: @>          index +
300: @>          1;
301:         // remove receiver from index mapping
302:         delete ownerToRollOverQueueIndex[_owner];
303:     }
304: }
```

## Tool used

Manual Review

## Recommendation

Consider not reordering the rollover queue items when delisting a rollover queue item. Instead, consider using an alternative data structure to store the queue items and the mapping from owner to queue item index.
