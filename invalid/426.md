Udsen

medium

# FEE-ON-TRANSFER TOKENS INTRODUCE ACCOUNTING ERRORS

## Summary

If fee-on-transfer token is used as the `IERC20` `asset` token of the SemiFungibleVault.sol contract, it could introduce accounting errors while depositing fresh funds to the `SemiFungibleVault` and when trying to send whole asset balance to the counter party vault when `endEpoch` or `depeg` events are triggered in the `ControllerPeggedAssetV2.sol` contract.

## Vulnerability Detail

Certain `ERC20` tokens charge a fee-on-transfer when trying to transfer assets from one account to another account.

Hence below three functionalities in the protocol can be affected:

### `deposit` function of the `SemiFungibleVault.sol` contract 

1. In the `SemiFungibleVault.sol` contract `asset` is declared as an `IERC20` token and hence this can support fee-on-transfer.
2. In the `deposit` function of the `SemiFungibleVault.sol` the deposited amount `assets` is directly used to mint the same `assets` number of tokens to the `reciever`.
3. Hence this could lead to less tokens being transferred to the vault than the amount of shares minted to the user.

 ### `sendTokens` function of the `VaultV2.sol` contract when triggered by `triggerDepeg` and `triggerEndEpoch` functions of the `ControllerPeggedAssetV2.sol` contract 

1. In the `triggerDepeg` and `triggerEndEpoch` functions the `setClaimTVL` of the respective premium vault or the collateral vault is called using the `finalTVL` value of the vault and deducting the respective fee from it.
2. And the same amount set in the `setClaimTVL` is then transferred to the counterparty vault using the `sendTokens` functions of the vault.
3. `sendTokens` function of the `VaultV2.sol` contract calls on the `safeTransfer` function of the `SemiFungibleVault.asset` to transfer the assets to the respective counterparty vault.
4. But if the fee-on-transfer is present here then the actually transfered amount to the counterparty vault will be less than the amount that was set in the `setClaimTVL`.

### `triggerDepeg` and `triggerEndEpoch` functions of the `ControllerPeggedAssetV2.sol` contract, trying to transfer entire asset amount to the counterparty vault 

1.  `depeg` or `EndEpoch` event occurs.
2.  The  `triggerDepeg` or `triggerEndEpoch` functions wants to transfer the entire assets of the respective `premium vault` or the `collateral vault` to the counterparty vault.
3.  But the vaults will not have enough funds to transfer due to the accounting error introduced during the deposits to the vaults. 
 

## Impact
Impact of the above two scenarios are described below:

### `deposit` function of the `SemiFungibleVault.sol` contract 

As it was described under vulnerability details this scenario leads to an accounting error in the relation between deposited assets amount and minted amount of shares to the receiver.
Hence the users who withdraws last will not be able to successfully withdraw, since there will be no assets left for withdrawal.

 ### `sendTokens` function of the `VaultV2.sol` contract when triggered by `triggerDepeg` and `triggerEndEpoch` functions of the `ControllerPeggedAssetV2.sol` contract 

The actually transfered asset amount to the counterparty vault will be less than the `ClaimTVL` value. Hence when calculating the `previewWithdraw` for the `withdrawal` of the assets, the returned `entitledAmount` will be respectively higher than deserved. Hence user will be able to withdraw more assets from the vault than he is entitled to. This will steal funds from the users who withdraw at the end.

### `triggerDepeg` and `triggerEndEpoch` functions of the `ControllerPeggedAssetV2.sol` contract, trying to transfer entire asset amount to the counterparty vault 

`premium vault` or the `collateral vault` will not have enough funds to transfer in the event a `depeg` or `endEpoch` occurs and requires total assets of vault to be transferred to the counter party vault. This error occurs due to the accounting error introduced during the `deposit` function of `SemiFungibleVault.sol` contract. 

## Code Snippet

```solidity
    function deposit(
        uint256 id,
        uint256 assets,
        address receiver
    ) public virtual {
        // Need to transfer before minting or ERC777s could reenter.
        asset.safeTransferFrom(msg.sender, address(this), assets);

        _mint(receiver, id, assets, EMPTY);

        emit Deposit(msg.sender, receiver, id, assets);
    }
```
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/SemiFungibleVault.sol#L59-L61

```solidity
    function sendTokens(
        uint256 _id,
        uint256 _amount,
        address _receiver
    ) external onlyController epochIdExists(_id) epochHasEnded(_id) {
        if (_amount > finalTVL[_id]) revert AmountExceedsTVL();
        if (epochAccounting[_id] + _amount > finalTVL[_id])
            revert AmountExceedsTVL();
        if (!whitelistedAddresses[_receiver] && _receiver != counterPartyVault)
            revert DestinationNotAuthorized(_receiver);
        epochAccounting[_id] += _amount;
        SemiFungibleVault.asset.safeTransfer(_receiver, _amount);
    }
```
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L319

```solidity
    function triggerDepeg(uint256 _marketId, uint256 _epochId) public {
       ...
        uint256 premiumTVL = premiumVault.finalTVL(_epochId);
        uint256 collateralTVL = collateralVault.finalTVL(_epochId);

        uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);
        uint256 collateralFee = calculateWithdrawalFeeValue(
            collateralTVL,
            epochFee
        );

        // avoid stack too deep error by avoiding local variables
        // uint256 premiumTVLAfterFee = premiumTVL - premiumFee;
        // uint256 collateralTVLAfterFee = collateralTVL - collateralFee;

        premiumVault.setClaimTVL(_epochId, collateralTVL - collateralFee);
        collateralVault.setClaimTVL(_epochId, premiumTVL - premiumFee);

        // send fees to treasury and remaining TVL to respective counterparty vault
        // strike price reached so premium is entitled to collateralTVL - collateralFee
        premiumVault.sendTokens(_epochId, premiumFee, treasury);
        premiumVault.sendTokens(
            _epochId,
            premiumTVL - premiumFee,
            address(collateralVault)
        );
        // strike price is reached so collateral is still entitled to premiumTVL - premiumFee but looses collateralTVL
        collateralVault.sendTokens(_epochId, collateralFee, treasury);
        collateralVault.sendTokens(
            _epochId,
            collateralTVL - collateralFee,
            address(premiumVault)
        );
      ...
}
```
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L93-L123

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L181-L192

## Tool used

VSCode and Manual Review

## Recommendation

Check the actual balance difference of the transferred in tokens 
Can use the `asset.balanceOf(address(this))` before and after the token transfer to calculate the actually transferred in amount and then mint the shares and set the `claimTVL` accordingly.