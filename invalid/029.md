Dug

medium

# A new controller could be unable to resolve epochs

## Summary

There are no checks that ensure that when the timelocker sets a new controller for a market, that the treasury used by the new controller is whitelisted by the associated vaults.

## Vulnerability Detail

In `VaultFactoryV2.sol`, the `changeController` function is used to change the controller of a market. This function takes a `_controller` address and calls the `changeController` function on both vaults with the address.

```solidity
function changeController(
    uint256 _marketId,
    address _controller
) public onlyTimeLocker controllerIsWhitelisted(_controller) {
    if (_controller == address(0)) revert AddressZero();

    address[2] memory vaults = marketIdToVaults[_marketId];

    if (vaults[0] == address(0) || vaults[1] == address(0)) {
        revert MarketDoesNotExist(_marketId);
    }

    IVaultV2(vaults[0]).changeController(_controller);
    IVaultV2(vaults[1]).changeController(_controller);

    emit ControllerChanged(_marketId, _controller, vaults[0], vaults[1]);
}
```

The vaults simply set the controller to the new address.

```solidity
function changeController(address _controller) public onlyFactory {
    if (_controller == address(0)) revert AddressZero();
    controller = _controller;
}
```

When resolving epochs, the controller moves fees from the vault to the treasury stored immutably within the controller.

However, when the controller attempts to use the vault's `sendTokens` function, the vault will check that the `_receiver` is whitelisted. 

```solidity
if (!whitelistedAddresses[_receiver] && _receiver != counterPartyVault)
    revert DestinationNotAuthorized(_receiver);
```

Because the controller's treasury is not necessarily whitelisted, the controller will not be able to move fees from the vault to the treasury.

## Impact

Calling `changeController` with a new controller that uses a treasury that is not already whitelisted by the vaults will result in the controller being unable to resolve epochs.

`triggerDepeg` and `triggerEndEpoch` will revert.

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultFactoryV2.sol#L281-L303

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L243-L250

## Tool used

Manual Review

## Recommendation

Ensure that a new controllers treasury is whitelisted by the vaults when `changeController` is called.

```diff
    function changeController(address _controller) public onlyFactory {
        if (_controller == address(0)) revert AddressZero();
+       whitelistedAddresses[ControllerPeggedAssetV2(_controller).treasury()] = true;
        controller = _controller;
    }
```
