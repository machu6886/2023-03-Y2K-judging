parsely

medium

# The function previewWithdraw is public and will always revert when being called before an epoch is resolved.

## Summary
The function **previewWithdraw** is public and will always revert when being called before an epoch is resolved.
## Vulnerability Detail
The **previewWithdraw** function's visibility is public and it can be called by any user at any stage. 
Due to the fact that during an epoch the **finalTVL** has not been set yet, when calling this function it reverts due to a division by ZERO error.
The finalTVL is only set once an epoch is resolved.
```solidity
    function resolveEpoch(uint256 _id)
        external
        onlyController
        epochIdExists(_id)
        epochHasStarted(_id)
    {
        if (epochResolved[_id]) revert EpochAlreadyEnded();
        epochResolved[_id] = true;
        finalTVL[_id] = totalAssets(_id);
    }
```
## PoC
Add this function to the VaultV2test.t.sol file and run the command.
```Text
forge test -vv --match-contract VaultV2Test --match-test testEarlyPreview
```
Function:
```solidity
    function testEarlyPreview() public {
        //----------------------------------//
        //   SETTING UP BASIC VAULT         //
        //----------------------------------//
        uint40 begin = uint40(block.timestamp);
        uint40 end = uint40(block.timestamp + 1 days);
        uint256 epochId = 1;
        helperSetEpoch(begin, end, epochId);
        vm.warp(begin + 1);       
        begin = uint40(block.timestamp);
        end = uint40(block.timestamp + 1 days);
        epochId = 3;
        helperSetEpoch(begin, end, epochId);
        //----------------------------------------//
        //  ADD SOME VALUE TO VAULT JUST TO SURE  //
        //----------------------------------------//
        // approve tokens to vault
        MintableToken(UNDERLYING).approve(address(vault), 10 ether);
        // deposit tokens
        vault.deposit(epochId, 10 ether, USER);
        // check balances
        assertEq(vault.balanceOf(USER ,epochId), 10 ether);

        //----------------------------------------------------//
        //  LET ANY USER CALL previewWithdraw as it's public  //
        //----------------------------------------------------//
        console.log("##################################################################");
        address couldBeAnyBody = address(0x012345678);
        vm.startPrank(couldBeAnyBody);
        console.log("## Without vm.expectRevert() this call would fail");
        vm.expectRevert();
        vault.previewWithdraw(epochId,5 ether);
        vm.stopPrank();

        //----------------------------------------------------//
        //  END THE EPOCH AND CALL AGAIN                      //
        //----------------------------------------------------//
        vm.startPrank(controller);
        vault.resolveEpoch(epochId);
        vm.stopPrank();

        vm.startPrank(couldBeAnyBody);
        console.log("## After the epoch is resolved a random user was able to successfully call");
        console.log("## The previewWithdraw value is now : " , vault.previewWithdraw(epochId,5 ether));
        vm.stopPrank();
        console.log("##################################################################");
        
    }
 ```
 This will give the output:
 ```Text
 Running 1 test for test/V2/VaultV2Test.t.sol:VaultV2Test
 PASS] testEarlyPreview() (gas: 502198)
 Logs:
 ##################################################################
 ## Without vm.expectRevert() this call would fail
 ## After the epoch is resolved a random user was able to successfully call
 ## The previewWithdraw value is now :  0
 ##################################################################

 Test result: ok. 1 passed; 0 failed; finished in 3.96ms
```
## Impact
Although it has no financial impact, functions should be checked that there is never a scenario where division by zero can occur without being correctly handled.
## Code Snippet
https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/VaultV2.sol#L357-L367
In VaultV2.sol the function ```previewWithdraw```
```solidity
    function previewWithdraw(uint256 _id, uint256 _assets)
        public
        view
        override(SemiFungibleVault)
        returns (uint256 entitledAmount)
    {
        // entitledAmount amount is derived from the claimTVL and the finalTVL
        // if user deposited 1000 assets and the claimTVL is 50% lower than finalTVL, the user is entitled to 500 assets
        // if user deposited 1000 assets and the claimTVL is 50% higher than finalTVL, the user is entitled to 1500 assets
        entitledAmount = _assets.mulDivDown(claimTVL[_id], finalTVL[_id]);
    }
 ```
## Tool used
Manual Review

## Recommendation
Add code to check if the epoch is resolved. Either revert or return zero if it has not yet been resolved.
If it needs to revert we could add the existing modifier **epochHasEnded(_id)** or we could change the code as below
```solidity
	if(epochResolved[_id]){
            entitledAmount = _assets.mulDivDown(claimTVL[_id], finalTVL[_id]);
        } 
```