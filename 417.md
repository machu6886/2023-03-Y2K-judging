berndartmueller

high

# Updating carousel vault rollover queue item sets the wrong index

## Summary

Updating a rollover queue item sets the wrong index in the `ownerToRollOverQueueIndex` mapping. This will mess up the mapping between users and their rollover queue items and cause issues with withdrawals and delisting rollover requests.

## Vulnerability Detail

Deposits into carousel vaults can be rolled over into the next epoch by calling the `Carousel.enlistInRollover` function. The `enlistInRollover` function adds the user's rollover request to the rollover queue (`rolloverQueue`) and stores the index (i.e., length of the queue) of the rollover queue item in the `ownerToRollOverQueueIndex` mapping.

If a user has already queued up a rollover, the existing item is updated without changing the index. However, the index is updated and set to the incorrect index of the last rollover queue item in line 268.

## Impact

1. The user will not be able to withdraw their (not rolled-over) assets from the vault due to the `notRollingOver` modifier [referencing the wrong rollover queue item](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L755).
2. Delisting a rollover will delist the wrong item and [causes another user's rollover request to be delisted instead](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L294)

## Code Snippet

[src/v2/Carousel/Carousel.enlistInRollover](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L268)

```solidity
238: function enlistInRollover(
239:     uint256 _epochId,
240:     uint256 _assets,
241:     address _receiver
242: ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
243:     // check if sender is approved by owner
244:     if (
245:         msg.sender != _receiver &&
246:         isApprovedForAll(_receiver, msg.sender) == false
247:     ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
248:     // check if user has enough balance
249:     if (balanceOf(_receiver, _epochId) < _assets)
250:         revert InsufficientBalance();
251:
252:     // check if user has already queued up a rollover
253:     if (ownerToRollOverQueueIndex[_receiver] != 0) {
254:         // if so, update the queue
255:         uint256 index = getRolloverIndex(_receiver);
256:         rolloverQueue[index].assets = _assets;
257:         rolloverQueue[index].epochId = _epochId;
258:     } else {
259:         // if not, add to queue
260:         rolloverQueue.push(
261:             QueueItem({
262:                 assets: _assets,
263:                 receiver: _receiver,
264:                 epochId: _epochId
265:             })
266:         );
267:     }
268: @>  ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
269:
270:     emit RolloverQueued(_receiver, _assets, _epochId);
271: }
```

## Tool used

Manual Review

## Recommendation

Consider only setting the index in `ownerToRollOverQueueIndex[_receiver]` if the user has not queued up a rollover yet:

```diff
  function enlistInRollover(
      uint256 _epochId,
      uint256 _assets,
      address _receiver
  ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
      // check if sender is approved by owner
      if (
          msg.sender != _receiver &&
          isApprovedForAll(_receiver, msg.sender) == false
      ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
      // check if user has enough balance
      if (balanceOf(_receiver, _epochId) < _assets)
          revert InsufficientBalance();
        // check if user has already queued up a rollover
      if (ownerToRollOverQueueIndex[_receiver] != 0) {
          // if so, update the queue
          uint256 index = getRolloverIndex(_receiver);
          rolloverQueue[index].assets = _assets;
          rolloverQueue[index].epochId = _epochId;
      } else {
          // if not, add to queue
          rolloverQueue.push(
              QueueItem({
                  assets: _assets,
                  receiver: _receiver,
                  epochId: _epochId
              })
          );
+         ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
      }
-     ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

      emit RolloverQueued(_receiver, _assets, _epochId);
  }
```
