berndartmueller

medium

# Risk-free hedge if depeg is triggered at the start of the epoch

## Summary

By using the greater than comparison operator (`>`) in the `epochHasNotStarted` modifier, users can risk-free hedge against a depeg if the depeg happens exactly at the start of the epoch (`EpochConfig.epochBegin`).

## Vulnerability Detail

A depeg event can be triggered as soon as the epoch begins (`EpochConfig.epochBegin`) by calling the `ControllerPeggedAssetV2.triggerDepeg` function. Deposits into vaults are intended only up until the epoch begins.

However, the `epochHasNotStarted` modifier in the `VaultV2` contract, used by the deposit functions, allows deposits until and including the epoch start (`EpochConfig.epochBegin`). If a depeg is going to be triggered at `block.timestamp = epoch.epochBegin`, users can still deposit into the premium vault and hedge against the depeg, risk-free.

## Impact

Users can risk-free hedge against a depeg if the depeg happens exactly at the start of the epoch (`EpochConfig.epochBegin`) because deposits are able until (inclusive) `EpochConfig.epochBegin`.

## Code Snippet

[src/v2/Controllers/ControllerPeggedAssetV2.sol - triggerDepeg()](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L71)

```solidity
File: src/v2/Controllers/ControllerPeggedAssetV2.sol
051: function triggerDepeg(uint256 _marketId, uint256 _epochId) public {
052:     address[2] memory vaults = vaultFactory.getVaults(_marketId);
053:
054:     if (vaults[0] == address(0) || vaults[1] == address(0))
055:         revert MarketDoesNotExist(_marketId);
056:
057:     IVaultV2 premiumVault = IVaultV2(vaults[0]);
058:     IVaultV2 collateralVault = IVaultV2(vaults[1]);
059:
060:     if (premiumVault.epochExists(_epochId) == false) revert EpochNotExist();
061:
062:     int256 price = getLatestPrice(premiumVault.token());
063:
064:     if (int256(premiumVault.strike()) <= price)
065:         revert PriceNotAtStrikePrice(price);
066:
067:     (uint40 epochStart, uint40 epochEnd, ) = premiumVault.getEpochConfig(
068:         _epochId
069:     );
070:
071: @>  if (uint256(epochStart) > block.timestamp) revert EpochNotStarted(); // @audit-info Depeg can be triggered exactly at `epochStart = block.timestamp`
...      // [...]
138: }
```

[src/v2/VaultV2.sol - deposit()](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L102)

The `epochHasNotStarted` modifier is used by the `deposit` function to prevent deposits after the epoch has started.

```solidity
093: function deposit(
094:     uint256 _id,
095:     uint256 _assets,
096:     address _receiver
097: )
098:     public
099:     virtual
100:     override(SemiFungibleVault)
101:     epochIdExists(_id)
102:     epochHasNotStarted(_id)
103:     nonReentrant
104: {
105:     if (_receiver == address(0)) revert AddressZero();
106:     SemiFungibleVault.asset.safeTransferFrom(
107:         msg.sender,
108:         address(this),
109:         _assets
110:     );
111:
112:     _mint(_receiver, _id, _assets, EMPTY);
113:
114:     emit Deposit(msg.sender, _receiver, _id, _assets);
115: }
```

[src/v2/VaultV2.sol - epochHasNotStarted()](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L433)

A epoch is considered to have started if `block.timestamp > epochConfig[_id].epochBegin`, which allows deposits until and inclusive `epochConfig[_id].epochBegin`.

```solidity
432: modifier epochHasNotStarted(uint256 _id) {
433: @>  if (block.timestamp > epochConfig[_id].epochBegin)
434:         revert EpochAlreadyStarted();
435:     _;
436: }
```

## Tool used

Manual Review

## Recommendation

Consider adapting the `epochHasNotStarted()` modifier by changing the comparison operator to `>=` instead of `>`.
