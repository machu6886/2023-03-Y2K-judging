8olidity

high

# Misconfigured _epochBegin Can Lead to Deposit Failure

## Summary
Misconfigured _epochBegin Can Lead to Deposit Failure
## Vulnerability Detail
Let's take a look at the call stack for _epochBegin. It is first set in `VaultFactoryV2::createEpoch()`:
```solidity
function createEpoch(
        uint256 _marketId,
        uint40 _epochBegin,
        uint40 _epochEnd,
        uint16 _withdrawalFee
    ) public onlyOwner returns (uint256 epochId, address[2] memory vaults) {
        vaults = marketIdToVaults[_marketId];

        if (vaults[0] == address(0) || vaults[1] == address(0)) {
            revert MarketDoesNotExist(_marketId);
        }

        if (_withdrawalFee == 0) revert FeeCannotBe0();

        if (!controllers[IVaultV2(vaults[0]).controller()])
            revert ControllerNotSet();
        if (!controllers[IVaultV2(vaults[1]).controller()])
            revert ControllerNotSet();

        epochId = getEpochId(_marketId, _epochBegin, _epochEnd);

        _setEpoch(
            EpochConfiguration(
                _epochBegin,
                _epochEnd,
                _withdrawalFee,
                _marketId,
                epochId,
                IVaultV2(vaults[0]),
                IVaultV2(vaults[1])
            )
        );
    }

```

Then it is passed into `VaultV2::setEpoch()`:
```solidity
function setEpoch(
        uint40 _epochBegin,
        uint40 _epochEnd,
        uint256 _epochId
    ) external onlyFactory {
        if (_epochId == 0 || _epochBegin == 0 || _epochEnd == 0)
            revert InvalidEpoch();
        if (epochExists[_epochId] == true) revert EpochAlreadyExists();

        if (_epochBegin >= _epochEnd) revert EpochEndMustBeAfterBegin();

        epochExists[_epochId] = true;

        epochConfig[_epochId] = EpochConfig({
            epochBegin: _epochBegin,
            epochEnd: _epochEnd,
            epochCreation: uint40(block.timestamp)
        });
        epochs.push(_epochId);
    }
```
Here,` _epochBegin` is only checked to ensure that it is less than `_epochEnd`. However, if `_epochBegin == block.timestamp`, it will satisfy this condition.
Now let's take a look at the Carouse.sol contract:
```solidity
function _deposit(
        uint256 _id,
        uint256 _assets,
        address _receiver
    ) internal {
        if (_id != 0) {
            uint256 assetsToDeposit = _assets;

            if (depositFee > 0) {
                (uint256 maxX, , uint256 minX) = getEpochConfig(_id);
                uint256 fee = _calculateFeePercent(int256(minX), int256(maxX));
                uint256 feeAmount = _assets.mulDivDown(fee, 10000);
                assetsToDeposit = _assets - feeAmount;
                _asset().safeTransfer(treasury, feeAmount);
            }
```

Here, `getEpochConfig()` is called to get `maxX` and `minX`, which correspond to `_epochBegin` and `epochCreation`. If we set `_epochBegin == block.timestamp`, i.e. `epochBegin == epochCreation == block.timestamp` (since `epochCreation` is set to `block.timestamp` by default)

```solidity
function _calculateFeePercent(int256 minX, int256 maxX)
        internal
        view
        returns (uint256 _y)
    {
        /**
         * Two Point Form
         * https://www.cuemath.com/geometry/two-point-form/
         * https://ethereum.stackexchange.com/a/143172
         */
        // minY will always be 0 thats why is (maxY - minY) shorten to maxY
        int256 maxY = int256(depositFee) * int256(FixedPointMathLib.WAD);
        _y = uint256( // cast to uint256
            ((((maxY) / (maxX - minX)) * (int256(block.timestamp) - maxX)) +
                maxY) / (int256(FixedPointMathLib.WAD)) // two point math // scale down
        );
    }
```
then we will have `maxX == minX`. As a result, when we call `_calculateFeePercent()`, `(maxX - minX) `will equal 0, causing a division by zero error and preventing `_deposit` from running properly.

## Impact
Misconfigured _epochBegin Can Lead to Deposit Failure
## Code Snippet

## Tool used
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L236-L238
Manual Review

## Recommendation
`_epochBegin` should be checked to ensure that it is greater than `block.timestamp`.