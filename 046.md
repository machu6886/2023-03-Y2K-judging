Bobface

medium

# Funds getting permanently stuck when invalid withdrawal fee is set

## Summary
When a withdrawal fee over 100 % is supplied (possibly by accident) to [`VaultFactoryV2.createEpoch()`](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultFactoryV2.sol#L137), funds deposited into the vault are permanently stuck due to an arithmetic underflow when the epoch is resolved.

## Vulnerability Detail
When a new epoch is created, the withdrawal fee is supplied in basis points (1/10_000) as `_withdrawalFee` parameter to [`VaultFactoryV2.createEpoch()`](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultFactoryV2.sol#L137). There are no checks performed on this value, which allows an invalid fee, i.e. over 100 %, to be supplied to the method. The contract will continue to function normally, accepting user deposits, until the epoch has reached its end or the strike price is reached.

Either [`ControllerPeggedAssetV2.triggerDepeg`](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L51) or [`ControllerPeggedAssetV2.triggerEndEpoch`](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L144) is executed to resolve the epoch. In both methods, the fee is calculated:

`triggerDepeg`:
```solidity
uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);
uint256 collateralFee = calculateWithdrawalFeeValue(
    collateralTVL,
    epochFee
);
```

`triggerEndEpoch`:
```solidity
uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);
```

This line underflows due to the fee being over 100 %, which causes the transaction to always revert.

## Impact
The epoch cannot be resolved anymore, and the vault is stuck in "open" for this epoch. User deposits cannot be withdrawn and are permanently stuck.

## Tool used

Manual Review

## Recommendation
Introduce a bounds check in [`VaultFactoryV2.createEpoch()`](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultFactoryV2.sol#L137):

```solidity
require(_withdrawalFee <= 10_000, "fee too high");
``` 

## Code Snippet
The PoC is written as Forge test. Paste the following code into `Earthquake/test/V2/FeeUnderflow.sol` and execute it with `forge test -m testFeeUnderflow`. 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "../../src/v2/VaultV2.sol";
import "../../src/v2/VaultFactoryV2.sol";
import "../../src/v2/Controllers/ControllerPeggedAssetV2.sol";

import "forge-std/Test.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

// PoC demonstrating funds being permanently stuck in the vault when an invalid fee is set.
contract FeeUnderflow is Test {
    ERC20 token;
    VaultV2 premium;
    VaultV2 collateral;
    uint256 epochId;
    uint256 marketId;
    VaultFactoryV2 vaultFactory;
    ControllerPeggedAssetV2 controller;

    address treasury = address(2);
    address receiver = address(3);
    uint256 startDate = block.timestamp + 100;
    uint256 endDate = block.timestamp + 1_000;
    
    function main() internal {
        // Main demo
        // Runs after setup in testFeeUnderflow is complete

        // Admin creates the epoch with the invalid fee (110 %)
        (epochId, ) = vaultFactory.createEpoch(
            marketId,
            uint40(startDate),
            uint40(endDate),
            11_000 // <----
        );
    
        // User deposits funds
        deal(address(token), address(this), 1 ether);
        premium.deposit(epochId, 1 ether, address(receiver));
    
        // Epoch ends
        vm.warp(endDate + 1);

        // The epoch cannot be resolved due to the underflow
        vm.expectRevert();
        controller.triggerEndEpoch(marketId, epochId);

        // Fee cannot be lowered anymore since the epoch already exists
        vm.expectRevert(VaultV2.EpochAlreadyExists.selector);
        vaultFactory.createEpoch(
            marketId,
            uint40(startDate),
            uint40(endDate),
            1 // <----
        );

        // Users cannot withdraw since the epoch is not resolved
        vm.prank(address(receiver));
        vm.expectRevert(VaultV2.EpochNotResolved.selector);
        premium.withdraw(epochId, 1 ether, address(receiver), address(receiver));
    }

    function testFeeUnderflow() external {
        // Setup the environment
        
        // Create the token
        token = new ERC20("TOKEN", "TOKEN");

        // Create the factory
        vaultFactory = new VaultFactoryV2(address(1), address(this), treasury);
        
        // Create the controller
        controller = new ControllerPeggedAssetV2(address(vaultFactory), address(1), treasury);

        // Whitelist the controller
        vaultFactory.whitelistController(address(controller));

        // Create the market
        VaultFactoryV2.MarketConfigurationCalldata memory marketConfiguration = 
            VaultFactoryV2.MarketConfigurationCalldata({
            token: address(token),
            strike: 1 ether,
            oracle: address(1),
            underlyingAsset: address(token),
            name: "",
            tokenURI: "",
            controller: address(controller)
        });
        (address _premium, address _collateral, uint256 _marketId) = vaultFactory.createNewMarket(marketConfiguration);
        premium = VaultV2(_premium);
        collateral = VaultV2(_collateral);
        marketId = _marketId;

        // Approve the token
        token.approve(address(premium), type(uint256).max);        

        // Execute main
        main();
    }
}
```