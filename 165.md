bin2chen

high

# epochHasNotStarted() equal to epochBegin should have started

## Summary
`deposit()` and `triggerDepeg()`  both can execute when block.timestam== epochBegin,  resulting in the opportunity to increase the percentage of premium without risk

## Vulnerability Detail
In the contract `VaultV2` and `Carousel`
Deposits will be limited when the epoch does not start,  use `epochHasNotStarted()`
```solidity
    modifier epochHasNotStarted(uint256 _id) {
        if (block.timestamp > epochConfig[_id].epochBegin) //<------- equal epochBegin is OK
            revert EpochAlreadyStarted();
        _;
    }
```
We can see that when block.timestamp==epochBegin is ok, `Not Started`

but in `epochHasStarted()`
```solidity
    modifier epochHasStarted(uint256 _id) {
        if (block.timestamp < epochConfig[_id].epochBegin)
            revert EpochNotStarted();
        _;
    }
```
if `block.timestamp==epochBegin`, is it also ok? `Has Started`?

These two are in conflict.

Let's look elsewhere.

In the contract `ControllerPeggedAssetV2`
anyone can call `triggerDepeg` if oracle price < strike()
Of course, we will restrict whether the epoch has already started
The code is as follows:
```solidity
    function triggerDepeg(uint256 _marketId, uint256 _epochId) public {
....
        int256 price = getLatestPrice(premiumVault.token());

        if (int256(premiumVault.strike()) <= price)
            revert PriceNotAtStrikePrice(price);

        (uint40 epochStart, uint40 epochEnd, ) = premiumVault.getEpochConfig(
            _epochId
        );

        if (uint256(epochStart) > block.timestamp) revert EpochNotStarted();   //<--------  block.timestamp == epochStart is OK
        if (block.timestamp > uint256(epochEnd)) revert EpochExpired();
```

we can see that when block.timestamp==epochBegin is able to call `triggerDepeg()`

The above scenario may lead to the risk of a flashloan attack in extreme cases:
A malicious user can monitor `epochBegin` and oracle price

When the current time is at `epochBegin`, and oracle price < strike()
the user can use flashloan to borrow a large funds, first `deposit()`, then `triggerDepeg()`, and then execute `withdraw()` to return funds to flashloan, in a transaction

thus taking up most of the compensation without risk

when equal  epochBegin should be already started, so we need to modify `epochHasNotStarted()`

## Impact
flashloan steal funds
## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L433
## Tool used

Manual Review

## Recommendation

```solidity
    modifier epochHasNotStarted(uint256 _id) {
-      if (block.timestamp > epochConfig[_id].epochBegin)
+      if (block.timestamp >= epochConfig[_id].epochBegin)
            revert EpochAlreadyStarted();
        _;
    }
```