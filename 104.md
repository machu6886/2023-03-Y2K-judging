shaka

high

# `enlistInRollover()` stores wrong index for receiver

## Summary

When a user that has already queued up a rollover calls again `Carousel.sol:enlistInRollover` he is incorrectly assigned the index of the last user in the queue.

## Vulnerability Detail

In `enlistInRollover()` it is checked if the `_receiver` address has already queued up a rollover. If that is the case, his entry in the queue array is updated. However, in line 268 the mapping `ownerToRollOverQueueIndex` that links the `_receiver` with the index of the `rolloverQueue` is incorrectly updated, pointing to the last item of the array.

```solidity
252  // check if user has already queued up a rollover
253  if (ownerToRollOverQueueIndex[_receiver] != 0) {
254      // if so, update the queue
255      uint256 index = getRolloverIndex(_receiver);
256      rolloverQueue[index].assets = _assets;
257      rolloverQueue[index].epochId = _epochId;
258  } else {
259      // if not, add to queue
260      rolloverQueue.push(
261          QueueItem({
262              assets: _assets,
263              receiver: _receiver,
264              epochId: _epochId
265          })
266      );
267  }
268  ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```

## Impact

The issue can lead to users having their assets locked and the rollover feature not working anymore.

The sequence of events is as follows:
- Alice has a balance of 1 Wad for an epoche and queues 0.5 Wad in a rollover. 
	```solidity
	rolloverQueue[0] = {assets: 0.5e18, receiver: Alice, epochId: 1}
	ownerToRollOverQueueIndex[Alice] = 1
	```
- Bob queues 10 Wad.
	```solidity
	rolloverQueue[0] = {assets: 0.5e18, receiver: Alice, epochId: 1}
	rolloverQueue[1] = {assets: 10e18, receiver: Bob, epochId: 1}
	ownerToRollOverQueueIndex[Alice] = 1
	ownerToRollOverQueueIndex[Bob] = 2
	```
- Alice decides to raise the amount and calls `enlistInRollover()` with an amount of 1 Wad.
	```solidity
	rolloverQueue[0] = {assets: 1e18, receiver: Alice, epochId: 1}
	rolloverQueue[1] = {assets: 10e18, receiver: Bob, epochId: 1}
	ownerToRollOverQueueIndex[Alice] = 2
	ownerToRollOverQueueIndex[Bob] = 2
	```
- Alice calls `delistInRollover()`. As `ownerToRollOverQueueIndex[Alice]` points to `rolloverQueue[1]`, is Bob's entry that is removed from the array. However, the `ownerToRollOverQueueIndex` mapping is updated for Alice.
	```solidity
	rolloverQueue[0] = {assets: 1e18, receiver: Alice, epochId: 1}
	ownerToRollOverQueueIndex[Bob] = 2
	```
 - Now Alice can call `withdraw()` or `safeTransferFrom()`, as she will pass the `notRollingOver` check while keeping her entry in `rolloverQueue` array. She can also call `enlistInRollover()` again, creating a new entry and keep repeating the process. Let's assume she withdraws the 1 Wad.
 - Bob has now his 10 Wad locked. Cannot withdraw as `ownerToRollOverQueueIndex[Bob] = 2`, so trying to fetch `rolloverQueue[1]` will result in `out of bounds` exception. Only if new entries are added to the queue he will be able to unlist his assets, but then another user will have the same issue.
 - When `mintDepositInQueue()` is executed by the relayer, it will revert as 1 Wad is tried to be burned from Alice's balance, which is 0 at this point.

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L238-L271

## Proof of concept

```solidity
function testEnlistInRolloverBug() public {
    uint40 _epochBegin = uint40(block.timestamp + 1 days);
    uint40 _epochEnd = uint40(block.timestamp + 2 days);
    uint256 _epochId = 2;

    vault.setEpoch(_epochBegin, _epochEnd, _epochId);

    // USER deposits 10 ether
    vm.startPrank(USER);
    IERC20(UNDERLYING).approve(address(vault), 100 ether);
    vault.deposit(_epochId, 10 ether, USER);
    // Enlists in rollover with 10 ether
    vault.enlistInRollover(_epochId, 10 ether, USER); // total enrolled: 10 ether
    vm.stopPrank();

    // USER2 deposits 10 ether
    uint256 user2Amount = 10 ether;
    vm.startPrank(USER2);
    IERC20(UNDERLYING).approve(address(vault), 10 ether);
    vault.deposit(_epochId, user2Amount, USER2);
    // Enlists in rollover with 10 ether
    vault.enlistInRollover(_epochId, 10 ether, USER2);
    vm.stopPrank();

    // USER enlist in rollover with 10 ether
    vm.startPrank(USER);
    vault.enlistInRollover(_epochId, 10 ether, USER);
    // Calls delistInRollover
    vault.delistInRollover(USER);
    // Enlists in rollover with 10 ether
    vault.enlistInRollover(_epochId, 10 ether, USER);
    vm.stopPrank();

    // USER3 deposits 10 ether
    vm.startPrank(USER3);
    IERC20(UNDERLYING).approve(address(vault), 10 ether);
    vault.deposit(_epochId, 10 ether, USER3);
    // Enlists in rollover with 1 ether
    vault.enlistInRollover(_epochId, 10 ether, USER3);
    vm.stopPrank();

    // USER enlist in rollover with 10 ether
    vm.startPrank(USER);
    vault.enlistInRollover(_epochId, 10 ether, USER);
    // Calls delistInRollover
    vault.delistInRollover(USER);
    vm.stopPrank();

    // resolve prev epoch with win
    vm.warp(block.timestamp + 2 days + 1 hours); // warp to one hour after prev epoch end
    vm.startPrank(controller);
    vault.resolveEpoch(_epochId);
    vault.setClaimTVL(_epochId, 33 ether);
    vm.stopPrank();

    // USER withdraws gains from previous epoch
    vm.startPrank(USER);
    vault.withdraw(_epochId, 10 ether, USER, USER);
    vm.stopPrank();

    // create second epoch
    _epochBegin = uint40(block.timestamp + 1 days);
    _epochEnd = uint40(block.timestamp + 2 days);
    _epochId ++;
    vault.setEpoch(_epochBegin, _epochEnd, _epochId);
    // Mint rollovers fails (USER is in queue but has no tokens to be burned)
    vm.expectRevert();
    vault.mintRollovers(_epochId, 100000);

    // USER3 cannot recover deposit
    vm.startPrank(USER3);
    vm.expectRevert();
    vault.delistInRollover(USER3);
    vm.expectRevert();
    vault.withdraw(_epochId, 10 ether, USER3, USER3);
    vm.stopPrank();
}
```

## Tool used

Manual Review

## Recommendation

Assign the new index to `ownerToRollOverQueueIndex[_receiver]` only if is a new entry.
```diff
  } else {
      // if not, add to queue
      rolloverQueue.push(
          QueueItem({
              assets: _assets,
              receiver: _receiver,
              epochId: _epochId
          })
      );
+     ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
  }
-  ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
```