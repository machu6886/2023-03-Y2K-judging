Aymen0909

high

# owner rollover queue index is always changed in `enlistInRollover`

## Summary

The user rollover queue index is always changed in the function `enlistInRollover` even if the user has already queued up a rollover, this will lead to some users having the same rollover index and thus they will override their rollover data (`assets` and `epochId`) when updating it, and this will also cause issue when minting the rollovers.

## Vulnerability Detail

The issue occurs in the `enlistInRollover` function below : 

```solidity
function enlistInRollover(
    uint256 _epochId,
    uint256 _assets,
    address _receiver
) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
    // check if sender is approved by owner
    if (
        msg.sender != _receiver &&
        isApprovedForAll(_receiver, msg.sender) == false
    ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
    // check if user has enough balance
    if (balanceOf(_receiver, _epochId) < _assets)
        revert InsufficientBalance();

    // check if user has already queued up a rollover
    if (ownerToRollOverQueueIndex[_receiver] != 0) {
        // if so, update the queue
        uint256 index = getRolloverIndex(_receiver);
        rolloverQueue[index].assets = _assets;
        rolloverQueue[index].epochId = _epochId;
    } else {
        // if not, add to queue
        rolloverQueue.push(
            QueueItem({
                assets: _assets,
                receiver: _receiver,
                epochId: _epochId
            })
        );
    }
    
    /** @audit
        This should only be updated if a new rollover is added to the queue
        And the rolloverQueue length changes 
    */
    ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

    emit RolloverQueued(_receiver, _assets, _epochId);
}
```

As you can see in the [Line 268](https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L268) the `ownerToRollOverQueueIndex[_receiver]` is changed even if the user has previously queued up a rollover and just wanted to update his `assets` and `epochId` values.

This will cause troubles for the protocol as users will override other previous users rollover data (meaning `assets` and `epochId` values), this can be illustrated in the following example : 

* Let's suppose : 

 user A has a rollover with index = 1
 
 user B has a rollover with index = 2
 
 The rolloverQueue has a length equal to 5
 
* If user A call the `enlistInRollover` function to change his rollover assets amount then the function after updating will change his index to the length of the rolloverQueue so now we have user A with index = 5

* Just after that if the user B also want to update his rollover assets amount then the function will update the data and also changes his index to the length of the rolloverQueue which is still equal to 5 as no new rollover was added, thus the user B will also have index = 5.

* We can notice that now both users have the same index which means that if one of the user changes again his rollover data the rollover data of the other user will also be changed, and this will also impact `mintRollovers` as some users will receive wrong amounts that they didn't wanted (less or more).

Note that this issue can happen easily if a certain number of users update their rollovers in a successive manner without the creation of a new one, as in this case all those users will end up with the same queue index.

## Impact

Users rollover data (mainly `assets` and `epochId` values) can be changed by other users intentionnally, and the `mintRollovers` will be impacted by that as some users will receive wrong assets amount.

## Code Snippet

https://github.com/Y2K-Finance/Earthquake/blob/736b2e1e51bef6daa6a5ecd1decb7d156316d795/src/v2/Carousel/Carousel.sol#L238-L271

## Tool used
Manual Review

## Recommendation
To avoid any issue the owner rollover queue index must only be updated if the user add a new rollover to the queue.

The `enlistInRollover` function can be modified as follows : 

```solidity
function enlistInRollover(
    uint256 _epochId,
    uint256 _assets,
    address _receiver
) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
    // check if sender is approved by owner
    if (
        msg.sender != _receiver &&
        isApprovedForAll(_receiver, msg.sender) == false
    ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
    // check if user has enough balance
    if (balanceOf(_receiver, _epochId) < _assets)
        revert InsufficientBalance();

    // check if user has already queued up a rollover
    if (ownerToRollOverQueueIndex[_receiver] != 0) {
        // if so, update the queue
        uint256 index = getRolloverIndex(_receiver);
        rolloverQueue[index].assets = _assets;
        rolloverQueue[index].epochId = _epochId;
    } else {
        /** @audit
            Only change ownerToRollOverQueueIndex if new rollover is added
        */
        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
        // if not, add to queue
        rolloverQueue.push(
            QueueItem({
                assets: _assets,
                receiver: _receiver,
                epochId: _epochId
            })
        );
    }

    emit RolloverQueued(_receiver, _assets, _epochId);
}
```
