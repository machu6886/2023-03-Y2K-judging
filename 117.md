ltyu

high

# Deposit in queue can be blocked

## Summary
When queuing a deposit, the receiver is not checked for implementation of `ERC1155.onERC1155Received`. This can break deposit queuing.

## Vulnerability Detail
The function `Carousel.deposit` can be used to deposit into a queue i.e. epoch 0. Later, a relayer can call `mintDepositInQueue` and mint the deposits in a batch. However, the `_mintShares()` function will always revert if any of the receivers in the queue are 1) contracts, and 2) don't implement `onERC1155Received`. Since the queue will never update (`.pop()` does not get called), there is no way to skip these non-compliant receivers, and the queue will be bricked.

## Impact
This is high impact because if any receivers that don't implement `onERC1155Received`, the deposits in the queue will be frozen. In other words, user deposits will be lost.

## Code Snippet
There is no `onERC1155Received` check when deposit is queued:
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L495-L499

`mintDepositInQueue` also does not check. When `_mintShares` is called, the call will revert:
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L310-L354

Here is a unit test. Add this to CarouselTest.t.sol, and run `forge test --match-test testDepositInQueueUnreceivable`
```solidity
function testDepositInQueueUnreceivable() public {
	uint40 _epochBegin = uint40(block.timestamp + 1 days);
	uint40 _epochEnd = uint40(block.timestamp + 2 days);
	uint256 _epochId = 1;
	uint256 _emissions = 100 ether;
	
	
	deal(emissionsToken, address(vault), 100 ether, true);
	vault.setEpoch(_epochBegin, _epochEnd, _epochId);
	vault.setEmissions( _epochId, _emissions);
	
	vm.startPrank(USER);
	IERC20(UNDERLYING).approve(address(vault), 10 ether);
	vault.deposit(0, 10 ether, USER);
	vm.stopPrank();
	
	
	uint256 _queueLength = 1;
	assertEq(vault.getDepositQueueLenght(), _queueLength);
	// test revert cases
	// should revert if epochId is 0 as this epoch is not supposed to minted ever
	vm.expectRevert(Carousel.InvalidEpochId.selector);
	vault.mintDepositInQueue(0, _queueLength);
	
	// should revert if epoch already started
	vm.warp(_epochBegin + 100);
	vm.expectRevert(VaultV2.EpochAlreadyStarted.selector);
	vault.mintDepositInQueue(_epochId, 1);
	
	
	vm.warp(_epochBegin - 1 days);
	// should revert if epoch does not exist
	vm.expectRevert(VaultV2.EpochDoesNotExist.selector);
	vault.mintDepositInQueue(3, 1);
	vm.startPrank(relayer);
	
	// setting operations to 5 should still only mint 1 as queue length is 1
	vault.mintDepositInQueue(_epochId, 5);
	vm.stopPrank();
	
	// test user balances
	assertEq(vault.balanceOf(USER, _epochId), 10 ether - relayerFee);
	assertEq(vault.balanceOfEmissions(USER, _epochId), 10 ether - relayerFee);
	
	// test relayer balance
	assertEq(IERC20(UNDERLYING).balanceOf(relayer), relayerFee * 1);
	  
	
	// deposit with multiple users
	_epochBegin = uint40(block.timestamp + 1 days);
	_epochEnd = uint40(block.timestamp + 2 days);
	_epochId = 2;
	_emissions = 1000 ether;
	  
	
	deal(emissionsToken, address(vault), 1000 ether, true);
	vault.setEpoch(_epochBegin, _epochEnd, _epochId);
	vault.setEmissions( _epochId, _emissions);
	  
	
	vm.startPrank(USER);
	IERC20(UNDERLYING).approve(address(vault), 10 ether);
	vault.deposit(0, 10 ether, USER);
	vm.stopPrank();
	
	
	// deposit for this contract, which is not an ERC1155 receiver
	deal(UNDERLYING, address(this), 1000 ether, true);
	vm.startPrank(address(this));
	IERC20(UNDERLYING).approve(address(vault), 10 ether);
	vault.deposit(0, 10 ether, address(this));
	vm.stopPrank();
	  
	
	_queueLength = 2;
	  
	
	assertEq(vault.getDepositQueueLenght(), _queueLength);
	// This will revert with ERC1155: transfer to non ERC1155Receiver implementer
	vault.mintDepositInQueue(_epochId, 230000);
}
```

## Tool used

Manual Review

## Recommendation
Consider checking for `onERC1155Received` when depositing in the queue, if the `receiver` is a contract.