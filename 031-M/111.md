Ruhum

medium

# Admin is able to steal user funds by increasing withdrawalFee to 100%

## Summary
By setting the withdrawalFee for a given epoch to 100% the admin is able to redirect user funds to the treasury. The project README clearly says that the admin *should not* be able to steal funds so I think this is a valid issue here: https://github.com/sherlock-audit/2023-03-Y2K-0xruhum#q-are-there-any-additional-protocol-roles-if-yes-please-explain-in-detail

## Vulnerability Detail
When an epoch is resolved through the Controller part of the vault's funds are sent to the protocol's treasury address. The amount is defined for each epoch by the `withdrawalFee` parameter set by the admin through the factory contract:
```sol
    function createEpoch(
        uint256 _marketId,
        uint40 _epochBegin,
        uint40 _epochEnd,
        uint16 _withdrawalFee
    ) public onlyOwner returns (uint256 epochId, address[2] memory vaults) {
        vaults = marketIdToVaults[_marketId];

        if (vaults[0] == address(0) || vaults[1] == address(0)) {
            revert MarketDoesNotExist(_marketId);
        }

        if (_withdrawalFee == 0) revert FeeCannotBe0();

        if (!controllers[IVaultV2(vaults[0]).controller()])
            revert ControllerNotSet();
        if (!controllers[IVaultV2(vaults[1]).controller()])
            revert ControllerNotSet();

        epochId = getEpochId(_marketId, _epochBegin, _epochEnd);

        _setEpoch(
            EpochConfiguration(
                _epochBegin,
                _epochEnd,
                _withdrawalFee,
                _marketId,
                epochId,
                IVaultV2(vaults[0]),
                IVaultV2(vaults[1])
            )
        );
    }
    
    function _setEpoch(EpochConfiguration memory _epochConfig) internal {
        _epochConfig.premium.setEpoch(
            _epochConfig.epochBegin,
            _epochConfig.epochEnd,
            _epochConfig.epochId
        );
        _epochConfig.collateral.setEpoch(
            _epochConfig.epochBegin,
            _epochConfig.epochEnd,
            _epochConfig.epochId
        );

        epochFee[_epochConfig.epochId] = _epochConfig.withdrawalFee;
        marketIdToEpochs[_epochConfig.marketId].push(_epochConfig.epochId);

        emit EpochCreated(
            _epochConfig.epochId,
            _epochConfig.marketId,
            _epochConfig.epochBegin,
            _epochConfig.epochEnd,
            address(_epochConfig.premium),
            address(_epochConfig.collateral),
            _epochConfig.premium.token(),
            _epochConfig.premium.strike(),
            _epochConfig.withdrawalFee
        );
    }
```
There are no checks implemented to prevent the admin from setting the withdrawal fee to 100%.

The Controller uses the withdrawal fee to determine the share of the vault's assets that should be sent to the treasury address:
```sol
        uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);
        uint256 collateralFee = calculateWithdrawalFeeValue(
            collateralTVL,
            epochFee
        );

        // avoid stack too deep error by avoiding local variables
        // uint256 premiumTVLAfterFee = premiumTVL - premiumFee;
        // uint256 collateralTVLAfterFee = collateralTVL - collateralFee;

        premiumVault.setClaimTVL(_epochId, collateralTVL - collateralFee);
        collateralVault.setClaimTVL(_epochId, premiumTVL - premiumFee);

        // send fees to treasury and remaining TVL to respective counterparty vault
        // strike price reached so premium is entitled to collateralTVL - collateralFee
        premiumVault.sendTokens(_epochId, premiumFee, treasury);
        premiumVault.sendTokens(
            _epochId,
            premiumTVL - premiumFee,
            address(collateralVault)
        );
        // strike price is reached so collateral is still entitled to premiumTVL - premiumFee but looses collateralTVL
        collateralVault.sendTokens(_epochId, collateralFee, treasury);
        collateralVault.sendTokens(
            _epochId,
            collateralTVL - collateralFee,
            address(premiumVault)
        );
```

The system doesn't allow the user to withdraw their funds before an epoch has finished. Thus, they are forced to pay the withdrawal fee.

## Impact
The admin is able to steal user funds by setting an epoch's withdrawal fee to 100%.

## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/VaultFactoryV2.sol#L137-L201
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#LL96-L123C11
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L170-L192
## Tool used

Manual Review

## Recommendation
Add a limit on the withdrawal fee, e.g.:

```sol
    function createEpoch(
        uint256 _marketId,
        uint40 _epochBegin,
        uint40 _epochEnd,
        uint16 _withdrawalFee
    ) public onlyOwner returns (uint256 epochId, address[2] memory vaults) {
        vaults = marketIdToVaults[_marketId];

        if (vaults[0] == address(0) || vaults[1] == address(0)) {
            revert MarketDoesNotExist(_marketId);
        }

        if (_withdrawalFee == 0) revert FeeCannotBe0();
       require(_withdrawalFee < 1000, "fee has to be less than 10%");
       // ...
    }
```
