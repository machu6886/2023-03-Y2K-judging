bin2chen

medium

# enlistInRollover() wrong set ownerToRollOverQueueIndex

## Summary
enlistInRollover() wrong set ownerToRollOverQueueIndex, so the user may modify other people's `rolloverQueue[index].asset`
## Vulnerability Detail
`enlistInRollover()` is used to modify or add `rolloverQueue`
The code is as follow:
```solidity
    function enlistInRollover(
        uint256 _epochId,
        uint256 _assets,
        address _receiver
    ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
        // check if sender is approved by owner
        if (
            msg.sender != _receiver &&
            isApprovedForAll(_receiver, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
        // check if user has enough balance
        if (balanceOf(_receiver, _epochId) < _assets)
            revert InsufficientBalance();

        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
            //<--------------not return
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
        }
        ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length; //<-----if modifty still call

        emit RolloverQueued(_receiver, _assets, _epochId);
    }
```
The above code has a problem:
 if `ownerToRollOverQueueIndex[_receiver] != 0`,  `ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length` still be called, resulting in setting the `ownerToRollOverQueueIndex[_receiver]`  to `rolloverQueue.length`, this is the index of other users.

When the index is wrong, the user can modify other people's `rolloverQueue[index].assets`ï¼Œ
Maliciously modifying `assets` to become large will cause `mintRollovers()` -> `burn()` will revert and `rolloverQueue` to be blocked

## Impact
block `mintRollovers()`
## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L268

## Tool used

Manual Review

## Recommendation
```solidity
    function enlistInRollover(
        uint256 _epochId,
        uint256 _assets,
        address _receiver
    ) public epochIdExists(_epochId) minRequiredDeposit(_assets) {
        // check if sender is approved by owner
        if (
            msg.sender != _receiver &&
            isApprovedForAll(_receiver, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _receiver);
        // check if user has enough balance
        if (balanceOf(_receiver, _epochId) < _assets)
            revert InsufficientBalance();

        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_receiver] != 0) {
            // if so, update the queue
            uint256 index = getRolloverIndex(_receiver);
            rolloverQueue[index].assets = _assets;
            rolloverQueue[index].epochId = _epochId;
        } else {
            // if not, add to queue
            rolloverQueue.push(
                QueueItem({
                    assets: _assets,
                    receiver: _receiver,
                    epochId: _epochId
                })
            );
+           ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;
        }
-       ownerToRollOverQueueIndex[_receiver] = rolloverQueue.length;

        emit RolloverQueued(_receiver, _assets, _epochId);
    }
```
