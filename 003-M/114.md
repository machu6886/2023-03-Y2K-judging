Ruhum

medium

# Attacker can DOS user's deposit by spamming the queue

## Summary
The queue processes entries using FILO. By spamming the queue with new deposits after a user has deposited you can lock up their funds.

## Vulnerability Detail
There's no way for a user to cancel their deposit. To get their funds out of the Carousel contract they **have** to get their deposit processed. But, since the queue uses FILO, there's the possibility of an attacker spamming the queue so that the user's deposit can't be reached.

The minimum deposit amount is `relayerFee`. That amount is sent to whoever executes the deposits in the queue. The most cost-effective solution is to spam the queue with a large number of minimum-size deposits. Then, you frontrun anybody's `mintDepositInQueue` execution so that you execute them yourself. That way you get back the deposits you made. Thus, the only cost (besides the opportunity cost of the attack) is the gas you spend. Since this protocol will be deployed in Arbitrum those are not really that high, see https://l2fees.info/

```sol
    function mintDepositInQueue(uint256 _epochId, uint256 _operations)
        external
        epochIdExists(_epochId)
        epochHasNotStarted(_epochId)
        nonReentrant
    {
        // make sure there is already a new epoch set
        // epoch has not started
        QueueItem[] memory queue = depositQueue;
        uint256 length = depositQueue.length;

        // dont allow minting if epochId is 0
        if (_epochId == 0) revert InvalidEpochId();

        if (length == 0) revert OverflowQueue();
        // relayers can always input a very big number to mint all deposit queues, without the need to read depostQueue length first
        if (_operations > length) _operations = length;

        // queue is executed from the tail to the head
        // get last index of queue
        uint256 i = length - 1;
        while ((length - _operations) <= i) {
            // this loop impelements FILO (first in last out) stack to reduce gas cost and improve code readability
            // changing it to FIFO (first in first out) would require more code changes and would be more expensive
            _mintShares(
                queue[i].receiver,
                _epochId,
                queue[i].assets - relayerFee
            );
            emit Deposit(
                msg.sender,
                queue[i].receiver,
                _epochId,
                queue[i].assets - relayerFee
            );
            depositQueue.pop();
            if (i == 0) break;
            unchecked {
                i--;
            }
        }

        emit RelayerMinted(_epochId, _operations);

        asset.safeTransfer(msg.sender, _operations * relayerFee);
    }
```
## Impact
DOS a user's deposit and cause their funds to be stuck.

## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L310-L355

## Tool used

Manual Review

## Recommendation
Allow a user to cancel their deposit.
