kenzo

high

# A null epoch can be resolved using `triggerEndEpoch`, thereby losing user funds

## Summary
The function to end a "normal" (not null and not depegged) epoch, `triggerEndEpoch`, does not check whether the epoch is a null epoch (= one of the vaults has no assets deposited).
Therefore, on a null epoch, somebody could call `triggerEndEpoch` instead of `triggerNullEpoch`, thereby making users lose their funds.

## Vulnerability Detail
In an epoch where for example no collateral has been deposited, instead of resolving the epoch using `triggerEndEpoch`, `triggerNullEpoch` should be called: it will let the premium depositors withdraw their funds, and not send them to the collateral vault.
**But `triggerEndEpoch` method, the "standard" resolve method, does not check whether the epoch is a null one.**
So somebody can call it instead of the correct `triggerNullEpoch`.
It [will set](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L181) the premium vault's claim TLV to 0, and set the collateral claim TLV as follows:
```solidity
        uint256 premiumTVLAfterFee = premiumTVL - premiumFee;
        uint256 collateralTVLAfterFee = collateralTVL + premiumTVLAfterFee;
        premiumVault.setClaimTVL(_epochId, 0);
        collateralVault.setClaimTVL(_epochId, collateralTVLAfterFee);
```
Therefore, if `triggerEndEpoch` is called instead of the correct `triggerEndEpoch`, it will set the claim TLVs as mentioned above.
So if there were premium depositors and not collateral depositors, the premium depositors will totally lose their funds (as claim TLV is 0).

## Impact
Lose of funds for users, as detailed above.
Although the griefer has nothing to gain by this, (unless he's a competitor?), the cost of the attack is low, and we've seen [such occurences](https://medium.com/cybermiles/i-accidentally-killed-it-and-evaporated-300-million-6b975dc1f76b) ("I accidently killed it") in the past.
Therefore, I believe this may be considered a high severity issue.

## Code Snippet
We can see that the `triggerEndEpoch` [function](https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Controllers/ControllerPeggedAssetV2.sol#L144) does not check whether the epoch is a null one. Therefore, it can be called instead of the correct function, leading to the impact described above.
```solidity
    function triggerEndEpoch(uint256 _marketId, uint256 _epochId) public {
        address[2] memory vaults = vaultFactory.getVaults(_marketId);

        if (vaults[0] == address(0) || vaults[1] == address(0))
            revert MarketDoesNotExist(_marketId);

        IVaultV2 premiumVault = IVaultV2(vaults[0]);
        IVaultV2 collateralVault = IVaultV2(vaults[1]);

        if (
            premiumVault.epochExists(_epochId) == false ||
            collateralVault.epochExists(_epochId) == false
        ) revert EpochNotExist();

        (, uint40 epochEnd, ) = premiumVault.getEpochConfig(_epochId);

        if (block.timestamp <= uint256(epochEnd)) revert EpochNotExpired();

        //require this function cannot be called twice in the same epoch for the same vault
        if (premiumVault.epochResolved(_epochId)) revert EpochFinishedAlready();
        if (collateralVault.epochResolved(_epochId))
            revert EpochFinishedAlready();

        premiumVault.resolveEpoch(_epochId);
        collateralVault.resolveEpoch(_epochId);

        uint256 epochFee = vaultFactory.getEpochFee(_epochId);

        uint256 premiumTVL = premiumVault.finalTVL(_epochId);
        uint256 collateralTVL = collateralVault.finalTVL(_epochId);

        uint256 premiumFee = calculateWithdrawalFeeValue(premiumTVL, epochFee);

        uint256 premiumTVLAfterFee = premiumTVL - premiumFee;
        uint256 collateralTVLAfterFee = collateralTVL + premiumTVLAfterFee;

        // strike price is not reached so premium is entiled to 0
        premiumVault.setClaimTVL(_epochId, 0);
        // strike price is not reached so collateral is entitled to collateralTVL + premiumTVLAfterFee
        collateralVault.setClaimTVL(_epochId, collateralTVLAfterFee);

        // send premium fees to treasury and remaining TVL to collateral vault
        premiumVault.sendTokens(_epochId, premiumFee, treasury);
        // strike price reached so collateral is entitled to collateralTVLAfterFee
        premiumVault.sendTokens(
            _epochId,
            premiumTVLAfterFee,
            address(collateralVault)
        );

        emit EpochResolved(
            _epochId,
            _marketId,
            VaultTVL(collateralTVLAfterFee, collateralTVL, 0, premiumTVL),
            false,
            block.timestamp,
            0
        );
    }
```

## Tool used

Manual Review

## Recommendation
Revert in `triggerEndEpoch` if it is a null epoch.