bin2chen

medium

# delistInRollover() disrupt the rolloverQueue order

## Summary
delistInRollover() disrupt the `rolloverQueue` order,the original last one may be ignore when `mintRollovers()`
## Vulnerability Detail
delistInRollover() use for delists from rollover queue
The code is as follows:
```solidity
    function delistInRollover(address _owner) public {
        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_owner] == 0) revert NoRolloverQueued();
        // check if sender is approved by owner
        if (
            msg.sender != _owner &&
            isApprovedForAll(_owner, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _owner);

        // swich the last item in the queue with the item to be removed
        uint256 index = getRolloverIndex(_owner);
        uint256 length = rolloverQueue.length;
        if (index == length - 1) {
            // if only one item in queue
            rolloverQueue.pop();
            delete ownerToRollOverQueueIndex[_owner];
        } else {
            // overwrite the item to be removed with the last item in the queue
            rolloverQueue[index] = rolloverQueue[length - 1]; //@audit<---------------put last to index
            // remove the last item in the queue
            rolloverQueue.pop();
            // update the index of prev last user ( mapping index is allways array index + 1)
            ownerToRollOverQueueIndex[rolloverQueue[index].receiver] =
                index +
                1;
            // remove receiver from index mapping
            delete ownerToRollOverQueueIndex[_owner];
        }
    }
```
From the above code we can see that if the removal is not the last one, the last one will be moved to the index to be removed
This adjusts the original order of the `rolloverQueue`.

But this order in `mintRollovers()` is quite important, because `mintRollovers()` uses `rolloverAccounting[_epochId]` to record the index of the last processed at the current `_epochId`.
If it is moved to before `rolloverAccounting[_epochId]`, then the queue item will not be processed at the current _epochId and it will be ignored at the current _epochId.
Example:
Assume that the current `rolloverQueue` is :
rolloverQueue[0]=jack assets =100
rolloverQueue[1]=jimmy assets = 100
rolloverQueue[2]=bob  assets = 100     <------rolloverAccounting[_epochId] = 2

1. Alice call `enlistInRollover()` ,so 
rolloverQueue[3]=Alice  assets = 200 

2. jimmy want to delist, and call `delistInRollover()`, so `rolloverQueue` became:
rolloverQueue[0]=jack assets =100     
rolloverQueue[1]=Alice assets = 200    <--------jimmy remove , set to Alice
rolloverQueue[2]=bob  assets = 100     <------rolloverAccounting[_epochId] still 2

3. anyone call `mintRollovers()`, nothing happen, because rolloverAccounting[_epochId]  == last

4. Alice's requests are ignored, and in more extreme cases, may be ignored forever

another scenario:
Assume that the current `rolloverQueue` :
rolloverQueue[0]=jack assets =100  <------ Assume rolloverAccounting[_epochId] = 1
1. if jack execute `delistInRollover` , then alice execute `enlistInRollover ` so `rolloverQueue` become:
rolloverQueue[0]=alice assets =100  <------rolloverAccounting[_epochId] still 1
2. Alice's requests are ignored, because rolloverAccounting[_epochId]==1



so recommended in `delistInRollover()` adjust rolloverAccounting[_epochId] if  rolloverAccounting[lastEpochId]> index

## Impact
delistInRollover() disrupt the rolloverQueue order,the original last one may be ignore
## Code Snippet
https://github.com/sherlock-audit/2023-03-Y2K/blob/main/Earthquake/src/v2/Carousel/Carousel.sol#L294-L302
## Tool used

Manual Review

## Recommendation
```solidity
    function delistInRollover(address _owner) public {
        // check if user has already queued up a rollover
        if (ownerToRollOverQueueIndex[_owner] == 0) revert NoRolloverQueued();
        // check if sender is approved by owner
        if (
            msg.sender != _owner &&
            isApprovedForAll(_owner, msg.sender) == false
        ) revert OwnerDidNotAuthorize(msg.sender, _owner);

        // swich the last item in the queue with the item to be removed
        uint256 index = getRolloverIndex(_owner);
        uint256 length = rolloverQueue.length;
        if (index == length - 1) {
            // if only one item in queue
            rolloverQueue.pop();
            delete ownerToRollOverQueueIndex[_owner];
        } else {
            // overwrite the item to be removed with the last item in the queue
            rolloverQueue[index] = rolloverQueue[length - 1];
            // remove the last item in the queue
            rolloverQueue.pop();
            // update the index of prev last user ( mapping index is allways array index + 1)
            ownerToRollOverQueueIndex[rolloverQueue[index].receiver] =
                index +
                1;
            // remove receiver from index mapping
            delete ownerToRollOverQueueIndex[_owner];

+           //adjuct rolloverAccounting , avoid the original last one being ignored
+           uint256 lastEpochId = epochs[epochs.length-1];
+           uint256 indexAccounting = rolloverAccounting[lastEpochId];
+           if(indexAccounting>index){
+               rolloverAccounting[lastEpochId] == index;
+           }            
        }
    }
```
